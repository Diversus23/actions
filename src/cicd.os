#Использовать cmdline
#Использовать logos
#Использовать "."

Перем Лог;
Перем ДополнительныеПараметры;

Функция ПолучитьПарсерКоманднойСтроки()
    
    Парсер = Новый ПарсерАргументовКоманднойСтроки();    
    МенеджерКомандПриложения.ЗарегистрироватьКоманды(Парсер);    
    Возврат Парсер;
    
КонецФункции

Функция ВыполнениеКоманды()

    ПараметрыЗапуска = РазобратьАргументыКоманднойСтроки();
    Если ПараметрыЗапуска = Неопределено ИЛИ ПараметрыЗапуска.Количество() = 0 Тогда
        Лог.Ошибка("Некорректные аргументы командной строки");
        МенеджерКомандПриложения.ПоказатьСправкуПоКомандам();
        Возврат 1;
    КонецЕсли;

    ПараметрыКоманды = Новый Соответствие;
    // ToDo Прочитать данные из файла настроек
    // ...
    // Вставляем данные после разбора аргументов командной строки
    Для Каждого КлючЗначение Из ПараметрыЗапуска.ЗначенияПараметров Цикл
        ПараметрыКоманды.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
    КонецЦикла;

    // Загрузка дополнительных параметров
    ПараметрыКоманды.Вставить("ДанныеПодключения", 
        ОбщегоНазначения.ДанныеПодключения(ПараметрыКоманды)); 
    
    Возврат ВыполнитьКоманду(ПараметрыЗапуска.Команда, ПараметрыКоманды);
    
КонецФункции

Функция ВыполнитьКоманду(Знач ИмяКоманды, Знач ПараметрыКоманды)
	
	Команда =  МенеджерКомандПриложения.ПолучитьКоманду(ИмяКоманды);
	Возврат Команда.ВыполнитьКоманду(ПараметрыКоманды, ДополнительныеПараметры);

КонецФункции

Функция РазобратьАргументыКоманднойСтроки()

    Парсер = ПолучитьПарсерКоманднойСтроки();    
    Возврат Парсер.Разобрать(АргументыКоманднойСтроки);

КонецФункции

/////////////////////////////////////////////////////////////////////////

Лог = Логирование.ПолучитьЛог(ОбщегоНазначения.ИмяЛогаСистемы());
УровеньЛога = УровниЛога.Отладка;

ДополнительныеПараметры = Новый Структура;
ДополнительныеПараметры.Вставить("Лог", Лог);

Попытка
	КодВозврата = ВыполнениеКоманды();
	ВременныеФайлы.Удалить();
	ЗавершитьРаботу(КодВозврата);
Исключение
    Лог.КритичнаяОшибка(ОписаниеОшибки());
    ВременныеФайлы.Удалить();
    ЗавершитьРаботу(МенеджерКомандПриложения.РезультатыКоманд().ОшибкаВремениВыполнения);
КонецПопытки;
