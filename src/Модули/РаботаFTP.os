#Использовать logos
#Использовать 1connector
#Использовать ftp

Перем Лог;

Функция НовоеFTPСоединение(Знач Настройки) Экспорт
	
	Возврат Новый FtpСоединение(
		Настройки.Сервер,
		Настройки.Порт,
		Настройки.ИмяПользователя,
		Настройки.Пароль, ,
		Истина,
		Настройки.Таймаут);
	
КонецФункции

Функция ПолучениеФайлов(Знач Параметры) Экспорт
	
	Результат = Новый Массив();
	
	Лог.Информация("Начало загрузки файлов с FTP");
		
	Попытка
		FTPСоединение = НовоеFTPСоединение(Параметры);
		FTPСоединение.УстановитьТекущийКаталог(Параметры.ПутьНаСервере);
		Лог.Информация("Текущий каталог на сервере <%1>", Параметры.ПутьНаСервере);
	Исключение
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Ошибка подключения к серверу FTP: %1", ОписаниеОшибки());
		Возврат Результат;
	КонецПопытки;
		
	МассивФайлов = FtpСоединение.НайтиФайлы(Параметры.ПутьНаСервере, Параметры.Маска, Истина); //Параметры.Рекурсивно
	Для Каждого ФайлНаСервере Из МассивФайлов Цикл

		ИмяФайла = ПреобразоватьВЛокальныйПуть(Параметры.ПутьНаСервере, 
			ФайлНаСервере.ПолноеИмя, Параметры.ПутьЛокально);
		НовыйФайл = Новый Файл(ИмяФайла);

		Если НЕ ФайловыеОперации.КаталогСуществует(НовыйФайл.Путь) Тогда
			ФайловыеОперации.ОбеспечитьПустойКаталог(НовыйФайл.Путь);
		КонецЕсли;
		
		FtpСоединение.Получить(ФайлНаСервере.ПолноеИмя, НовыйФайл.ПолноеИмя);
		Результат.Добавить(НовыйФайл.ПолноеИмя);
		Лог.Информация("Получен файл ""%1"" >> ""%2""", ФайлНаСервере.ПолноеИмя, НовыйФайл.ПолноеИмя);

	КонецЦикла;
	
	Лог.Информация("Завершение загрузки файлов с FTP");

	Возврат Результат;
		
КонецФункции

Функция ПреобразоватьВЛокальныйПуть(Знач ПутьНаСервере, Знач ФайлНаСервере, Знач ЛокальныйПуть)

	// ПутьНаСервере = "/"
	// ФайлНаСервере = "/journalext/index.html"
	// ЛокальныйПуть = "C:\Temp"
	// ====
	// Результат	 = "C:\Temp\journalext\index.html"

	Лог.Отладка("ПутьНаСервере: %1
				|ФайлНаСервере: %2
				|ЛокальныйПуть: %3", ПутьНаСервере, ФайлНаСервере, ЛокальныйПуть);

	Результат = ФайлНаСервере;
	Если СтрНачинаетсяС(ФайлНаСервере, ПутьНаСервере) Тогда
		Результат = Сред(Результат, СтрДлина(ПутьНаСервере) + 1);
	КонецЕсли;

	Лог.Отладка("Результат (удалили путь на сервере): %1", Результат);

	РазделительОС = ПолучитьРазделительПути();
	Если РазделительОС <> "/" Тогда
		Результат = СтрЗаменить(Результат, "/", РазделительОС);
	КонецЕсли;
	
	Лог.Отладка("Результат (удалили разделитель): %1", Результат);

	Результат = ОбъединитьПути(ЛокальныйПуть, Результат);

	Возврат Результат;

КонецФункции

Лог = ПараметрыСистемы.Лог();