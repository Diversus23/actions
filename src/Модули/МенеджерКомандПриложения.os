Перем мИсполнителиКоманд;

Процедура ЗарегистрироватьКоманды(Знач Парсер) Экспорт
	
	мИсполнителиКоманд = Новый Соответствие;
	
	ОбщегоНазначения.РегистрацияГлобальныхПараметровКоманд(Парсер);
	
	ДобавитьКоманду("help", "КомандаСправкаПоПараметрам", Парсер);
	ДобавитьКоманду("version", "КомандаВерсия", Парсер);
	ДобавитьКоманду("zip", "КомандаУправлениеZIP", Парсер);
	ДобавитьКоманду("unzip", "КомандаУправлениеZIP", Парсер);
	ДобавитьКоманду("createibfile", "КомандаСоздатьИнформационнуюБазу1С", Парсер);
	ДобавитьКоманду("createibserver", "КомандаСоздатьИнформационнуюБазу1С", Парсер);
	ДобавитьКоманду("checkconfig", "КомандаПроверитьСинтаксис", Парсер);
	ДобавитьКоманду("obfuscation", "КомандаОбфускация", Парсер);
	ДобавитьКоманду("autodoc", "КомандаАвтодокументация", Парсер);
	
КонецПроцедуры

Функция ПолучитьКоманду(Знач ИмяКоманды) Экспорт
	
	КлассРеализации = мИсполнителиКоманд[ИмяКоманды];
	Если КлассРеализации = Неопределено Тогда
		ВызватьИсключение "Неверная операция. Команда '" + ИмяКоманды + "' не предусмотрена";
	КонецЕсли;
	
	Возврат КлассРеализации;
	
КонецФункции

Функция ВыполнитьКоманду(Знач ИмяКоманды, Знач ПараметрыКоманды) Экспорт
	
	Команда = ПолучитьКоманду(ИмяКоманды);
	Возврат Команда.ВыполнитьКоманду(ПараметрыКоманды);
	
КонецФункции

Функция РезультатыКоманд() Экспорт
	
	РезультатыКоманд = Новый Структура;
	РезультатыКоманд.Вставить("НеРеализовано", 255);
	РезультатыКоманд.Вставить("Успех", 0);
	РезультатыКоманд.Вставить("НеверныеПараметры", 5);
	РезультатыКоманд.Вставить("ОшибкаВремениВыполнения", 1);
	
	Возврат РезультатыКоманд;
	
КонецФункции

Функция КодВозвратаКоманды(Знач Команда) Экспорт
	
	Возврат Число(Команда);
	
КонецФункции

Процедура ДобавитьКоманду(Знач ИмяКоманды, Знач КлассРеализации, Знач Парсер)
	
	РеализацияКоманды = Новый(КлассРеализации);
	РеализацияКоманды.ЗарегистрироватьКоманду(ИмяКоманды, Парсер);
	мИсполнителиКоманд.Вставить(ИмяКоманды, РеализацияКоманды);
	
КонецПроцедуры

Процедура ПоказатьСправкуПоКомандам() Экспорт
	
	ВыполнитьКоманду("help", Новый Соответствие);
	
КонецПроцедуры

Функция ТекстовоеОписаниеКоманд() Экспорт	

    Парсер = Новый ПарсерАргументовКоманднойСтроки();
    МенеджерКомандПриложения.ЗарегистрироватьКоманды(Парсер);
	ВозможныеКоманды = Парсер.СправкаВозможныеКоманды();
	ПС = Символы.ПС;
	ДС = ПС + ПС;

	Результат = "# Описание команд" + ДС;

	Результат = Результат + "| Команда | Описание |" + ПС
		+ "| --- | --- |" + ПС;
	Для Каждого Команда Из ВозможныеКоманды Цикл
		Результат = Результат 
			+ "| [`" + Команда.Команда + "`](#" + Команда.Команда + ")"+ " | " + Команда.Пояснение + " |" + ПС;
	КонецЦикла;

	Результат = Результат + ПС;

	// Таблица = Новый ТаблицаЗначений;
	// Таблица.Колонки.Добавить("Имя");
	// Таблица.Колонки.Добавить("ЭтоФлаг");
	// Таблица.Колонки.Добавить("Пояснение");
	// Таблица.Колонки.Добавить("ЭтоГлобальныйПараметр");
	// Таблица.Колонки.Добавить("ЭтоКоллекция");

	Для Каждого Команда Из ВозможныеКоманды Цикл
		Результат = Результат 
			+ "## " + Команда.Команда + ДС
			+ Команда.Пояснение + "." + ДС
			+ "**Параметры команды**" + ДС;

		ТекстПараметры = "";
		Для Каждого Параметр Из Команда.Параметры Цикл
			ТекстПараметры = ТекстПараметры
				+ "**" + Параметр.Имя + "** " + Параметр.Пояснение + "." + Символы.ПС;
		КонецЦикла;

		Результат = Результат + ТекстПараметры;
		Если НЕ ПустаяСтрока(ТекстПараметры) Тогда
			Результат = Результат + ПС;
		КонецЕсли;

	КонецЦикла;

	Возврат Результат;

КонецФункции