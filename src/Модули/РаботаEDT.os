#Использовать logos

// Лог системы
Перем Лог;

#Область ПрограммныйИнтерфейс

// Создает объект общего модуля конфигурации EDT
// Параметры:
//	- КаталогПроекта - Строка - каталог проекта с исходниками EDT
//	- ИмяМодуля - Строка - имя модуля, экземпляр которого необходимо создать
//
// Возвращаемое значение:
//	ОбщийМодуль, Неопределено - созданный экземпляр общего модуля
//
Функция СоздатьОбъектОбщегоМодуляEDT(Знач КаталогПроекта, Знач ИмяМодуля) Экспорт
	
	Если НЕ ЗначениеЗаполнено(КаталогПроекта) Тогда
		Возврат Неопределено;
	КонецЕсли;
	Если НЕ ФайловыеОперации.КаталогСуществует(КаталогПроекта) Тогда
		Возврат Неопределено;
	КонецЕсли;

	КаталогПроекта = ФайловыеОперации.АбсолютныйПуть(КаталогПроекта);

	ОбщиеМодули = ОбъединитьПути(КаталогПроекта, "src", "CommonModules");
	ОбщийМодуль = ОбъединитьПути(ОбщиеМодули, ИмяМодуля, "Module.bsl");

	Если НЕ ФайловыеОперации.ФайлСуществует(ОбщийМодуль) Тогда
		Возврат Неопределено;
	КонецЕсли;

	ПодключитьСценарий(ОбщийМодуль, ИмяМодуля);
	Результат = Вычислить(СтрШаблон("Новый %1()", ИмяМодуля));
	Возврат Результат;

КонецФункции

// Проверяет установку Ring
//
// Возвращаемое значение:
//	Булево - EDT установлено
//
Функция ПроверитьУстановкуRing() Экспорт

	Возврат ЗначениеЗаполнено(ОбщегоНазначения.НайтиПриложениеВСистеме("ring"));

КонецФункции

// Проверяет установку Java
//
// Возвращаемое значение:
//	Булево - Java установлено
//
Функция ПроверитьУстановкуJava() Экспорт

	Возврат ЗначениеЗаполнено(ОбщегоНазначения.НайтиПриложениеВСистеме("java"));

КонецФункции

// Получает массив установленных версий EDT
//
// Возвращаемое значение:
//	Массив из Структура: массив
//		* Версия - Строка - версия EDT
//		* Путь - Строка - путь к EDT
//
Функция УстановленныеВерсииEDT() Экспорт

	Лог = ПараметрыСистемы.Лог();
	Результат = Новый Массив();
	
	МассивПутейУстановки = Новый Массив();
	Если ОбщегоНазначения.ЭтоWindows() Тогда
		КаталогAPPDATA = ПолучитьПеременнуюСреды("LOCALAPPDATA");
		КаталогПрограммWindows_x64 = ПолучитьПеременнуюСреды("ProgramW6432");
		
		МассивПутейУстановки.Добавить(ОбъединитьПути(КаталогПрограммWindows_x64, "1C\1CE\components"));
		МассивПутейУстановки.Добавить(ОбъединитьПути(КаталогAPPDATA, "1C\1cedtstart\installations"));
		МассивПутейУстановки.Добавить("C:\edt\1cedtstart\installations");
		ИмяEdtCli = "1cedt.exe";
	Иначе
		МассивПутейУстановки.Добавить(ФайловыеОперации.АбсолютныйПуть("/opt/1C/1CE/components"));
		ИмяEdtCli = "1cedt";
	КонецЕсли;

	Для Каждого Путь Из МассивПутейУстановки Цикл
		Если ФайловыеОперации.КаталогСуществует(Путь) Тогда			
			ШаблонВерсии = "product.version=(\d+\.\d+.\d+|\d+\.\d+)";
			МассивКаталогов = НайтиФайлы(Путь, ИмяEdtCli, Истина);
			Для Каждого Файл Из МассивКаталогов Цикл
				АбсолютныйПуть = ФайловыеОперации.КаталогФайла(Файл.ПолноеИмя);
				ФайлConfig = ОбъединитьПути(АбсолютныйПуть, "configuration", "config.ini");
				Если ФайловыеОперации.ФайлСуществует(ФайлConfig) Тогда
					Текст = ФайловыеОперации.ПрочитатьТекстФайла(ФайлConfig);
					// product.version=2024.1.2
					Массив = ОбщегоНазначения.НайтиПоРегулярномуВыражению(Текст, ШаблонВерсии);
					Если Массив.Количество() > 1 Тогда
						Структура = Новый Структура("Версия, Путь", Массив[1], АбсолютныйПуть);
						Результат.Добавить(Структура);
					КонецЕсли;
				КонецЕсли;				
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;

	Возврат Результат;

КонецФункции

// Возвращает последнюю установленную версию EDT
//
// Возвращаемое значение:
//	Структура, Неопределено - последняя версия:
//		* Версия - Строка - версия EDT
//		* Путь - Строка - путь к EDT
//
Функция ПоследняяУстановленнаяВерсияEDT() Экспорт
	Массив = УстановленныеВерсииEDT();
	
	Если Массив.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;

	Результат = Массив[0];
	РезультатВес = ОбщегоНазначения.ВесВерсии(Результат.Версия);
	Для Каждого Структура Из Массив Цикл
		Вес = ОбщегоНазначения.ВесВерсии(Структура.Версия);
		Если РезультатВес < Вес Тогда
			Результат = Структура;
			РезультатВес = Вес;
		КонецЕсли;
	КонецЦикла; 

	Возврат Результат;

КонецФункции

// Получает путь к версии EDT
//
// Параметры:
//	Версия - Строка - версия EDT. В формате 2024.1.1
//
// Возвращаемое значение:
//	Строка - путь к версии EDT
//
Функция ПолучитьПутьКВерсииEDT(Знач Версия) Экспорт

	Массив = УстановленныеВерсииEDT();
	
	Если Массив.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;

	Для Каждого Структура Из Массив Цикл
		Если Версия = Структура.Версия Тогда
			Возврат Структура.Путь;
		КонецЕсли;
	КонецЦикла;

	Возврат Неопределено;

КонецФункции

// Проверяет установку Edt
//
// Возвращаемое значение:
//	Булево - Java установлено
//
Функция ПроверитьУстановкуEdt() Экспорт

	Возврат УстановленныеВерсииEDT().Количество() > 0;

КонецФункции

// Экспортирует проект EDT в формат XML 1С с помощью Ring
//
// Параметры:
//  Параметры    - Структура     - структура для экспорта
//		* КаталогПроекта - Строка - Каталог исходников EDT
//		* КаталогЭкспорта - Строка - Каталог куда надо экспортировать
//		* ВерсияEDT - Строка - версия EDT
//
Процедура ЭкспортВXML1CEdtCli(Знач Параметры) Экспорт

	Лог = ПараметрыСистемы.Лог();
	Лог.Информация("Начало экспорта проекта 1C:Enterprise Development Tools в XML-файлы конфигурации 1С:Предприятия");

	ВерсииEDT = УстановленныеВерсииEDT();

	Если ВерсииEDT.Количество() = 0 Тогда
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Не установлен 1С:EDT");
	КонецЕсли;

	Версия = "";
	Путь  = Неопределено;
	Если НЕ ПустаяСтрока(Параметры.ВерсияEDT) Тогда
		Версия = Параметры.ВерсияEDT;
		Путь = ПолучитьПутьКВерсииEDT(Версия);
	КонецЕсли;

	Если Путь = Неопределено Тогда
		ПоследняяВерсия = ПоследняяУстановленнаяВерсияEDT();
		Путь = ПоследняяВерсия.Путь;
		Версия = ПоследняяВерсия.Версия;
	КонецЕсли;

	Лог.Информация("Версия EDT для экспорта: %1", Версия);

	ИсполнимыйФайл = ОбъединитьПути(Путь, "1cedtcli");
	Если ОбщегоНазначения.ЭтоWindows() Тогда
		ИсполнимыйФайл = ИсполнимыйФайл + ".exe";
	КонецЕсли;

	Если НЕ ФайловыеОперации.ФайлСуществует(ИсполнимыйФайл) Тогда
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Не найден файл 1cedtcli (%1)", ИсполнимыйФайл);
	КонецЕсли;

	КаталогПроекта = ФайловыеОперации.УдалитьПоследнийРазделительПути(Параметры.КаталогПроекта);
	КаталогЭкспорта = ФайловыеОперации.УдалитьПоследнийРазделительПути(Параметры.КаталогЭкспорта);
	КаталогПространства = Параметры.КаталогПространства;
	УдалитьКаталогПространства = Ложь;
	Если НЕ ЗначениеЗаполнено(КаталогПространства) Тогда
		КаталогПространства = ФайловыеОперации.ОбеспечитьВременныйКаталог();
		УдалитьКаталогПространства = Истина;
	КонецЕсли;
	КаталогПространства = ФайловыеОперации.УдалитьПоследнийРазделительПути(КаталогПространства);

	Массив = Новый Массив();
	Массив.Добавить("-data");
	Массив.Добавить("""" + КаталогПространства + """");
	Массив.Добавить("-command");
	Массив.Добавить("export");
	Массив.Добавить("--project");
	Массив.Добавить("""" + КаталогПроекта + """");
	Массив.Добавить("--configuration-files");
	Массив.Добавить("""" + КаталогЭкспорта + """");

	ФайловыеОперации.ОбеспечитьПустойКаталог(КаталогЭкспорта);
	
	Команда = Новый КонтролируемыйПроцесс();
	Команда.УстановитьКаталогКонтроляОткрытыхПриложений(Параметры.КаталогКонтроляОткрытыхПриложений);
	Команда.УстановитьКоманду(ИсполнимыйФайл);
	Команда.ДобавитьПараметры(Массив);
	Команда.ВывестиСтрокуЗапуска();
	Команда.Исполнить();

	// Анализируем есть ли файл
	ТекстСОшибкой = "";
	ФайлКонфигурации = ОбъединитьПути(КаталогЭкспорта, "Configuration.xml");
	Если НЕ ФайловыеОперации.ФайлСуществует(ФайлКонфигурации) Тогда
		ФайлЛогов = ОбъединитьПути(КаталогПространства, ".metadata", ".log");
		ТекстСОшибкой = "Нет информации";
		Если ФайловыеОперации.ФайлСуществует(ФайлЛогов) Тогда
			ТекстСОшибкой = ФайловыеОперации.ПрочитатьТекстФайла(ФайлЛогов);
		КонецЕсли;
	КонецЕсли;

	Если УдалитьКаталогПространства Тогда
		Лог.Информация("Удаление временного каталога пространства");
		УдалитьФайлы(КаталогПространства);
		Лог.Информация("Временный каталог пространства удален");
	КонецЕсли;

	Если НЕ ПустаяСтрока(ТекстСОшибкой) Тогда
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Экспорт проекта выполнен с ошибками. Проверьте параметр --edtversion.
												|Информация лога:
												|%1", ТекстСОшибкой);
	Иначе
		Лог.Информация("Экспорт проекта выполнен успешно");
	КонецЕсли;

КонецПроцедуры

// Экспортирует проект EDT в формат XML 1С с помощью Ring
//
// Параметры:
//  Параметры    - Структура     - структура для экспорта
//		* КаталогПроекта - Строка - Каталог исходников EDT
//		* КаталогЭкспорта - Строка - Каталог куда надо экспортировать
//		* КаталогПространства - Строка - Каталог пространства EDT
//		* ВерсияEDT - Строка - версия EDT
//
Процедура ЭкспортВXML1CRing(Знач Параметры) Экспорт
	
	Лог = ПараметрыСистемы.Лог();
	Лог.Информация("Начало экспорта проекта 1C:Enterprise Development Tools в XML-файлы конфигурации 1С:Предприятия");

	Если НЕ ПроверитьУстановкуRing() Тогда
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Не найдена установленная утилита ring");
	КонецЕсли;

	// ring edt@%env.EDT_VERSION% workspace export
	//	--project "%system.teamcity.build.workingDir%\it"
	//	--configuration-files "%system.teamcity.build.workingDir%\build\config"
	//	--workspace-location "%system.teamcity.build.workingDir%"
	
	// Обязательное удаления слешей в конце пути
	КаталогПроекта = ФайловыеОперации.УдалитьПоследнийРазделительПути(Параметры.КаталогПроекта);
	КаталогЭкспорта = ФайловыеОперации.УдалитьПоследнийРазделительПути(Параметры.КаталогЭкспорта);
	КаталогПространства = Параметры.КаталогПространства;
	УдалитьКаталогПространства = Ложь;
	Если НЕ ЗначениеЗаполнено(КаталогПространства) Тогда
		КаталогПространства = ФайловыеОперации.ОбеспечитьВременныйКаталог();
		УдалитьКаталогПространства = Истина;
	КонецЕсли;
	КаталогПространства = ФайловыеОперации.УдалитьПоследнийРазделительПути(КаталогПространства);

	Массив = Новый Массив();
	Если ЗначениеЗаполнено(Параметры.ВерсияEDT) Тогда
		Массив.Добавить("edt@" + Параметры.ВерсияEDT);
	Иначе
		Массив.Добавить("edt");
	КонецЕсли;
	Массив.Добавить("workspace export");
	Массив.Добавить("--project");
	Массив.Добавить("""" + КаталогПроекта + """");
	Массив.Добавить("--configuration-files");
	Массив.Добавить("""" + КаталогЭкспорта + """");
	Массив.Добавить("--workspace-location");
	Массив.Добавить("""" + КаталогПространства + """");

	ФайловыеОперации.ОбеспечитьПустойКаталог(КаталогЭкспорта);
	
	Команда = Новый КонтролируемыйПроцесс();
	Команда.УстановитьКаталогКонтроляОткрытыхПриложений(Параметры.КаталогКонтроляОткрытыхПриложений);
	Команда.УстановитьКоманду("ring");
	Команда.ДобавитьПараметры(Массив);
	Команда.ВывестиСтрокуЗапуска();
	Команда.Исполнить();

	// Анализируем есть ли файл
	ТекстСОшибкой = "";
	ФайлКонфигурации = ОбъединитьПути(КаталогЭкспорта, "Configuration.xml");
	Если НЕ ФайловыеОперации.ФайлСуществует(ФайлКонфигурации) Тогда
		ФайлЛогов = ОбъединитьПути(КаталогПространства, ".metadata", ".log");
		ТекстСОшибкой = "Нет информации";
		Если ФайловыеОперации.ФайлСуществует(ФайлЛогов) Тогда
			ТекстСОшибкой = ФайловыеОперации.ПрочитатьТекстФайла(ФайлЛогов);
		КонецЕсли;
	КонецЕсли;

	Если УдалитьКаталогПространства Тогда
		Лог.Информация("Удаление временного каталога пространства");
		УдалитьФайлы(КаталогПространства);
		Лог.Информация("Временный каталог пространства удален");
	КонецЕсли;

	Если НЕ ПустаяСтрока(ТекстСОшибкой) Тогда
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Экспорт проекта выполнен с ошибками. Проверьте параметр --edtversion.
												|Информация лога:
												|%1", ТекстСОшибкой);
	Иначе
		Лог.Информация("Экспорт проекта выполнен успешно");
	КонецЕсли;

КонецПроцедуры

// Получает из проекта EDT версию конфигурации.
//
// Параметры:
//	КаталогПроекта - Строка - Каталог исходников EDT
//
// Возвращаемое значение:
//	Строка - строка с версией. Пример: 3.1.16.6
//
Функция ВерсияКонфигурацииИзПроекта(Знач КаталогПроекта) Экспорт

	Описание = ОписаниеКонфигурацииИзПроекта(КаталогПроекта);
	Возврат Описание.Версия;

КонецФункции

// Получает структуру описания конфигуарции из проекта EDT
//
// Параметры:
//	КаталогПроекта - Строка - Каталог исходников EDT
//
// Возвращаемое значение:
//	Структура - описание конфигурации
//		Версия - Строка - строка с версией. Пример: 3.1.16.6
//
Функция ОписаниеКонфигурацииИзПроекта(Знач КаталогПроекта) Экспорт

	Результат = Новый Структура();

	Файлы = НайтиФайлы(КаталогПроекта, "Configuration.mdo", Истина);
	Для Каждого мФайл Из Файлы Цикл
		Если мФайл.ЭтоФайл() Тогда
			Текст = ФайловыеОперации.ПрочитатьТекстФайла(мФайл.ПолноеИмя);

			ШаблоныВерсий = Новый Массив;
			ШаблоныВерсий.Добавить("\d+\.\d+\.\d+\.\d+");
			ШаблоныВерсий.Добавить("\d+\.\d+\.\d+");
			ШаблоныВерсий.Добавить("\d+\.\d+");
			ШаблоныВерсий.Добавить("\d+");

			Файлы = НайтиФайлы(КаталогПроекта, "Configuration.mdo", Истина);
			Для Каждого мФайл Из Файлы Цикл
				Если мФайл.ЭтоФайл() Тогда
					Текст = ФайловыеОперации.ПрочитатьТекстФайла(мФайл.ПолноеИмя);

					// Версия
					Для Каждого Шаблон Из ШаблоныВерсий Цикл
						ШаблонВерсии = СтрШаблон("<version>(%1)<\/version>", Шаблон);
						Массив = ОбщегоНазначения.НайтиПоРегулярномуВыражению(Текст, ШаблонВерсии);
						Если Массив.Количество() > 1 Тогда				
							Результат.Вставить("Версия", Массив[1]);
							Прервать;
						КонецЕсли;
					КонецЦикла;

					// Имя
					Массив = ОбщегоНазначения.НайтиПоРегулярномуВыражению(Текст, "<name>(.*)<\/name>");
					Если Массив.Количество() > 1 Тогда				
						Результат.Вставить("Имя", Массив[1]);
					КонецЕсли;

					// Поставщик
					Массив = ОбщегоНазначения.НайтиПоРегулярномуВыражению(Текст, "<vendor>(.*)<\/vendor>");
					Если Массив.Количество() > 1 Тогда				
						Результат.Вставить("Поставщик", ПреобразоватьСтроку(Массив[1]));
					КонецЕсли;

					// Синоним конфигурации
					// Очень коряво сделано... Надо переделать регулярное выражение
					Результат.Вставить("Синонимы", Новый Массив);					
					ЧастьТекст = Лев(Текст, СтрНайти(Текст, "<version>"));
					Массив = ОбщегоНазначения.НайтиПоРегулярномуВыражению(ЧастьТекст, 					
						"<synonym>[\s\S]*?<key>(.*)<\/key>[\s\S]*?<value>(.*)<\/value>[\s\S]*?<\/synonym>");
					Если Массив.Количество() > 1 Тогда
						Индекс = 1;
						Пока Индекс < Массив.Количество() Цикл
							Синоним = Новый Структура("Язык, Синоним", Массив[Индекс], Массив[Индекс + 1]);
							Результат.Синонимы.Добавить(Синоним);
							Индекс = Индекс + 3;
						КонецЦикла;
					КонецЕсли;					

				КонецЕсли;
			КонецЦикла;			

		КонецЕсли;
	КонецЦикла;

	Возврат Результат;

КонецФункции

// Получает из проекта EDT версию отчета/обработки.
//
// Параметры:
//	КаталогПоиск - Строка - Каталог исходников EDT
//
// Возвращаемое значение:
//	Строка - строка с версией. Пример: 3.1.16.6
//
Функция ВерсияОтчетаОбработкиИзПроекта(Знач КаталогПоиск) Экспорт

	Файлы = НайтиФайлы(КаталогПоиск, "ObjectModule.bsl", Истина);
	Для Каждого мФайл Из Файлы Цикл
		Если мФайл.ЭтоФайл() Тогда
			Текст = ФайловыеОперации.ПрочитатьТекстФайла(мФайл.ПолноеИмя);

			Массив = ОбщегоНазначения.НайтиПоРегулярномуВыражению(Текст, """(\d+.\d+.\d+.\d+)""");
			Если Массив.Количество() > 1 Тогда
				Возврат Массив[1];
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Возврат "";

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПреобразоватьСтроку(Знач Строка)

	Результат = СтрЗаменить(Строка, "&quot;", """");
	Возврат Результат;

КонецФункции

#КонецОбласти

Лог = ПараметрыСистемы.Лог();