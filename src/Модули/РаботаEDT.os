// Лог системы
Перем Лог;

#Область ПрограммныйИнтерфейс

// Экспортирует проект EDT в формат XML 1С
//
// Параметры:
//  Параметры    - Структура     - структура для экспорта
//		* КаталогПроекта - Строка - Каталог исходников EDT
//		* КаталогЭкспорта - Строка - Каталог куда надо экспортировать
//		* КаталогПространства - Строка - Каталог пространства EDT
//		* ВерсияEDT - Строка - версия EDT
//
Процедура ЭкспортВXML1C(Знач Параметры) Экспорт
	
	// ring edt@%env.EDT_VERSION% workspace export
	//	--project "%system.teamcity.build.workingDir%\it"
	//	--configuration-files "%system.teamcity.build.workingDir%\build\config"
	//	--workspace-location "%system.teamcity.build.workingDir%"
	
	// Обязательное удаления слешей в конце пути
	Параметры.КаталогПроекта = ФайловыеОперации.УдалитьПоследнийРазделительПути(Параметры.КаталогПроекта);
	Параметры.КаталогЭкспорта = ФайловыеОперации.УдалитьПоследнийРазделительПути(Параметры.КаталогЭкспорта);
	Параметры.КаталогПространства = ФайловыеОперации.УдалитьПоследнийРазделительПути(Параметры.КаталогПространства);

	Лог = ПараметрыСистемы.Лог();
	Лог.Информация("Начало экспорта проекта 1C:Enterprise Development Tools в XML-файлы конфигурации 1С:Предприятия");
	Массив = Новый Массив();
	Массив.Добавить("ring");
	Если ЗначениеЗаполнено(Параметры.ВерсияEDT) Тогда
		Массив.Добавить("edt@" + Параметры.ВерсияEDT);
	Иначе
		Массив.Добавить("edt");
	КонецЕсли;
	Массив.Добавить("workspace export");
	Массив.Добавить("--project");
	Массив.Добавить("""" + Параметры.КаталогПроекта + """");
	Массив.Добавить("--configuration-files");
	Массив.Добавить("""" + Параметры.КаталогЭкспорта + """");
	Массив.Добавить("--workspace-location");
	Массив.Добавить("""" + Параметры.КаталогПространства + """");
	
	СтрокаЗапуска = СтрСоединить(Массив, " ");
	Лог.Информация("Выполняем команду:
					|================
					|%1
					|================", СтрокаЗапуска);
	
	КодВозврата = 0;
	ЗапуститьПриложение(СтрокаЗапуска, , Истина, КодВозврата);
	
	Если КодВозврата > 0 Тогда
		Лог.Информация("Экспорт проекта выполнен с ошибками");
	Иначе
		Лог.Информация("Экспорт проекта выполнен успешно. Если этого не произошло, проверьте параметр --edtversion");
	КонецЕсли;

КонецПроцедуры

// Получает из проекта EDT версию конфигурации.
//
// Параметры:
//	КаталогПроекта - Строка - Каталог исходников EDT
//	ЧиселВВерсии - Число - количество частей версии "1.0.0.0" => 4 части, "1.0" => 2 части и т.д. (от 1 до 4)
//
// Возвращаемое значение:
//	Строка - строка с версией
Функция ВерсияКонфигурацииИзПроекта(Знач КаталогПроекта, Знач ЧиселВВерсии = 4) Экспорт

	Если НЕ ЗначениеЗаполнено(ЧиселВВерсии) Тогда
		ЧиселВВерсии = 4;
	КонецЕсли;

	ШаблонВерсии = "";
	Если ЧиселВВерсии = 4 Тогда
		ШаблонВерсии = "[0-9]*\.[0-9]*\.[0-9]*\.[0-9]*";
	ИначеЕсли ЧиселВВерсии = 3 Тогда
		ШаблонВерсии = "[0-9]*\.[0-9]*\.[0-9]*";
	ИначеЕсли ЧиселВВерсии = 2 Тогда
		ШаблонВерсии = "[0-9]*\.[0-9]*";
	ИначеЕсли ЧиселВВерсии = 1 Тогда
		ШаблонВерсии = "[0-9]*";
	Иначе
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Опция ""--count"" - может быть числом от 1 до 4");
	КонецЕсли;

	ШаблонВерсии = "<version>(" + ШаблонВерсии + ")<\/version>";

	Версия = "";
	Файлы = НайтиФайлы(КаталогПроекта, "Configuration.mdo", Истина);
	Для Каждого мФайл Из Файлы Цикл		
		Если мФайл.ЭтоФайл() Тогда
			ТекстовыйДокумент = Новый ТекстовыйДокумент();
			ТекстовыйДокумент.Прочитать(мФайл.ПолноеИмя);
			Массив = ОбщегоНазначения.НайтиПоРегулярномуВыражению(ТекстовыйДокумент.ПолучитьТекст(), ШаблонВерсии);
			Если Массив.Количество() > 1 Тогда				
				Версия = Массив[1];
				Прервать;
			КонецЕсли;			
		КонецЕсли;
	КонецЦикла;	

	Возврат Версия;

КонецФункции

#КонецОбласти

Лог = ПараметрыСистемы.Лог();