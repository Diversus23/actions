#Использовать logos

// Лог системы
Перем Лог;

#Область ПрограммныйИнтерфейс

// Экспортирует проект EDT в формат XML 1С
//
// Параметры:
//  Параметры    - Структура     - структура для экспорта
//		* КаталогПроекта - Строка - Каталог исходников EDT
//		* КаталогЭкспорта - Строка - Каталог куда надо экспортировать
//		* КаталогПространства - Строка - Каталог пространства EDT
//		* ВерсияEDT - Строка - версия EDT
//
Процедура ЭкспортВXML1C(Знач Параметры) Экспорт
	
	// ring edt@%env.EDT_VERSION% workspace export
	//	--project "%system.teamcity.build.workingDir%\it"
	//	--configuration-files "%system.teamcity.build.workingDir%\build\config"
	//	--workspace-location "%system.teamcity.build.workingDir%"
	
	// Обязательное удаления слешей в конце пути
	КаталогПроекта = ФайловыеОперации.УдалитьПоследнийРазделительПути(Параметры.КаталогПроекта);
	КаталогЭкспорта = ФайловыеОперации.УдалитьПоследнийРазделительПути(Параметры.КаталогЭкспорта);
	КаталогПространства = Параметры.КаталогПространства;
	УдалитьКаталогПространства = Ложь;
	Если НЕ ЗначениеЗаполнено(КаталогПространства) Тогда
		КаталогПространства = ФайловыеОперации.ОбеспечитьВременныйКаталог();
		УдалитьКаталогПространства = Истина;
	КонецЕсли;
	КаталогПространства = ФайловыеОперации.УдалитьПоследнийРазделительПути(КаталогПространства);

	Лог = ПараметрыСистемы.Лог();
	Лог.Информация("Начало экспорта проекта 1C:Enterprise Development Tools в XML-файлы конфигурации 1С:Предприятия");
	Массив = Новый Массив();
	Массив.Добавить("ring");
	Если ЗначениеЗаполнено(Параметры.ВерсияEDT) Тогда
		Массив.Добавить("edt@" + Параметры.ВерсияEDT);
	Иначе
		Массив.Добавить("edt");
	КонецЕсли;
	Массив.Добавить("workspace export");
	Массив.Добавить("--project");
	Массив.Добавить("""" + КаталогПроекта + """");
	Массив.Добавить("--configuration-files");
	Массив.Добавить("""" + КаталогЭкспорта + """");
	Массив.Добавить("--workspace-location");
	Массив.Добавить("""" + КаталогПространства + """");

	ФайловыеОперации.ОбеспечитьПустойКаталог(КаталогЭкспорта);
	
	СтрокаЗапуска = СтрСоединить(Массив, " ");
	Лог.Информация("Выполняем команду:
					|================
					|%1
					|================", СтрокаЗапуска);
	
	КодВозврата = 0;
	ЗапуститьПриложение(СтрокаЗапуска, , Истина, КодВозврата);

	// Анализируем есть ли файл
	ТекстСОшибкой = "";
	ФайлКонфигурации = ОбъединитьПути(КаталогЭкспорта, "Configuration.xml");
	Если НЕ ФайловыеОперации.ФайлСуществует(ФайлКонфигурации) Тогда
		ФайлЛогов = ОбъединитьПути(КаталогПространства, ".metadata", ".log");
		ТекстСОшибкой = "Нет информации";
		Если ФайловыеОперации.ФайлСуществует(ФайлЛогов) Тогда
			ТекстСОшибкой = ФайловыеОперации.ПрочитатьТекстФайла(ФайлЛогов);
		КонецЕсли;
	КонецЕсли;

	Если УдалитьКаталогПространства Тогда
		Лог.Информация("Удаление временного каталога пространства");
		УдалитьФайлы(КаталогПространства);
		Лог.Информация("Временный каталог пространства удален");
	КонецЕсли;

	Если НЕ ПустаяСтрока(ТекстСОшибкой) Тогда
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Экспорт проекта выполнен с ошибками. Проверьте параметр --edtversion.
												|Информация лога:
												|%1", ТекстСОшибкой);
	Иначе
		Лог.Информация("Экспорт проекта выполнен успешно");
	КонецЕсли;

КонецПроцедуры

// Получает из проекта EDT версию конфигурации.
//
// Параметры:
//	КаталогПроекта - Строка - Каталог исходников EDT
//	ЧиселВВерсии - Число - количество частей версии "1.0.0.0" => 4 части, "1.0" => 2 части и т.д. (от 1 до 4)
//
// Возвращаемое значение:
//	Строка - строка с версией
Функция ВерсияКонфигурацииИзПроекта(Знач КаталогПроекта, Знач ЧиселВВерсии = 4) Экспорт

	ЧетыреЧасти = 4;
	ТриЧасти = 3;
	ДвеЧасти = 2;
	ОднаЧасть = 1;

	Если НЕ ЗначениеЗаполнено(ЧиселВВерсии) Тогда
		ЧиселВВерсии = ЧетыреЧасти;
	КонецЕсли;

	ШаблонВерсии = "";
	Если ЧиселВВерсии = ЧетыреЧасти Тогда
		ШаблонВерсии = "[0-9]*\.[0-9]*\.[0-9]*\.[0-9]*";
	ИначеЕсли ЧиселВВерсии = ТриЧасти Тогда
		ШаблонВерсии = "[0-9]*\.[0-9]*\.[0-9]*";
	ИначеЕсли ЧиселВВерсии = ДвеЧасти Тогда
		ШаблонВерсии = "[0-9]*\.[0-9]*";
	ИначеЕсли ЧиселВВерсии = ОднаЧасть Тогда
		ШаблонВерсии = "[0-9]*";
	Иначе
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Опция ""--count"" - может быть числом от 1 до 4");
	КонецЕсли;

	ШаблонВерсии = "<version>(" + ШаблонВерсии + ")<\/version>";

	Версия = "";
	Файлы = НайтиФайлы(КаталогПроекта, "Configuration.mdo", Истина);
	Для Каждого мФайл Из Файлы Цикл		
		Если мФайл.ЭтоФайл() Тогда
			Текст = ФайловыеОперации.ПрочитатьТекстФайла(мФайл.ПолноеИмя);
			Массив = ОбщегоНазначения.НайтиПоРегулярномуВыражению(Текст, ШаблонВерсии);
			Если Массив.Количество() > 1 Тогда				
				Версия = Массив[1];
				Прервать;
			КонецЕсли;			
		КонецЕсли;
	КонецЦикла;	

	Возврат Версия;

КонецФункции

#КонецОбласти

Лог = ПараметрыСистемы.Лог();