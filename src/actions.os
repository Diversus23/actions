#Использовать logos
#Использовать cli
#Использовать "."

// Обработчик выполнения команды
//
// Параметры:
//   КомандаПриложения - КомандаПриложения - Выполняемая команда
//
Процедура ВыполнитьКоманду(Знач КомандаПриложения) Экспорт
    
    КомандаПриложения.ВывестиСправку();
    
КонецПроцедуры

Процедура ВыполнитьПриложение()
    
    Приложение = Новый КонсольноеПриложение(ПараметрыСистемы.ИмяПриложения(), "Приложение для CI/CD");
    Приложение.Версия("version", ПараметрыСистемы.Версия());
    
    Приложение.ДобавитьКоманду("changelog",
        "Команды работы с CHANGELOG.md",
        Новый КомандыРаботыСChangeLog());
    
    Приложение.ДобавитьКоманду("checksum",
        "Выводит в консоль контрольную сумма файла или директории в формате MD5",
        Новый КомандаКонтрольнаяСумма());

    Приложение.ДобавитьКоманду("obfuscation",
        "Обфускация (запутывание) кода на языке 1С",
        Новый КомандыОбфускации1С());
        
    Приложение.Опция("v verbose", Ложь, "Вывод отладочной информации в процессе выполнения")
    .Флаговый()
    .ВОкружении("ACTIONS_VERBOSE");
    
    Приложение.Опция("s settings", "", "Путь к файлу настроек (по умолчанию: ../env.json)")
    .ТСтрока()
    .ВОкружении("ACTIONS_SETTINGS");
    
    Приложение.УстановитьОсновноеДействие(ЭтотОбъект);
    
    Приложение.Запустить(АргументыКоманднойСтроки);
    
КонецПроцедуры

// Функция - проверяет, что приложение запущено в режиме тестирования
//
// Возвращаемое значение:
//   Булево  - Истина - приложение запущено в режиме тестирования
//
Функция ЭтоТест()
    
    Попытка
        Возврат ЭтотОбъект.ЭтоТест;
    Исключение
        Возврат Ложь;
    КонецПопытки;
    
КонецФункции // ЭтоТест()


/////////////////////////////////////////////////////////////////////////

ПараметрыСистемы.Лог = Логирование.ПолучитьЛог(ПараметрыСистемы.ИмяЛогаСистемы());
ПараметрыСистемы.ЭтоWindows = ОбщегоНазначения.ЭтоWindows();

Попытка
    
    ВыполнитьПриложение();
    
Исключение
    
    ПараметрыСистемы.Лог.КритичнаяОшибка(ОписаниеОшибки());
    ВременныеФайлы.Удалить();
    
    Если ЭтоТест() Тогда
        ВызватьИсключение ОписаниеОшибки();
    Иначе
        ЗавершитьРаботу(1);
    КонецЕсли;
    
КонецПопытки;