#Использовать v8runner

#Область ПрограммныйИнтерфейс

// Процедура - устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект описание команды
//
Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Опция("path p", "",
		"Путь где будет осуществлен поиск и замена тегов в feature-файлах (обязательный)")
		.ТСтрока();

	Команда.Опция("from f", "",
		"Исходный тег, который надо заменить во feature-файлах (обязательный)")
		.ТСтрока();

	Команда.Опция("to t", "",
		"Тег, которым будет заменен исходный тег ""--from"" во feature-файлах (обязательный)")
		.ТСтрока();

КонецПроцедуры // ОписаниеКоманды()

// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	
	Параметры = ПолучитьСтруктуруПараметров(Команда);

	Лог = ПараметрыСистемы.Лог();
	Лог.Информация("Начало замены тегов в каталоге <%1>. Тег ""%2"" заменяем на ""%3""", Параметры.Путь,
		Параметры.ТегИсточник, Параметры.ТегЗамены);

	Файлы = НайтиФайлы(Параметры.Путь, "*.feature", Истина);
	Для Каждого Файл Из Файлы Цикл
		ЗаменитьТег(Файл.ПолноеИмя, Параметры.ТегИсточник, Параметры.ТегЗамены);
	КонецЦикла;

	Лог.Информация("Завершение замены тегов в feature-файлах");
	
КонецПроцедуры // ВыполнитьКоманду()

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСтруктуруПараметров(Знач Команда)
	
	ЧтениеОпций = Новый ЧтениеОпцийКоманды(Команда);
	ВыводОтладочнойИнформации = ЧтениеОпций.ЗначениеОпции("verbose");
	ПараметрыСистемы.УстановитьРежимОтладки(ВыводОтладочнойИнформации);
	
	ПараметрыКоманды = Новый Структура();
	ПараметрыКоманды.Вставить("Путь", ЧтениеОпций.ЗначениеОпции("path", Истина));
	ПараметрыКоманды.Вставить("ТегИсточник", ЧтениеОпций.ЗначениеОпции("from", Истина));
	ПараметрыКоманды.Вставить("ТегЗамены", ЧтениеОпций.ЗначениеОпции("to", Истина));
	Если НЕ СтрНачинаетсяС(ПараметрыКоманды.ТегИсточник, "@") Тогда
		ПараметрыКоманды.ТегИсточник = "@" + ПараметрыКоманды.ТегИсточник;
	КонецЕсли;
	Если НЕ СтрНачинаетсяС(ПараметрыКоманды.ТегЗамены, "@") Тогда
		ПараметрыКоманды.ТегЗамены = "@" + ПараметрыКоманды.ТегЗамены;
	КонецЕсли;

	Возврат ПараметрыКоманды;
	
КонецФункции // ПолучитьСтруктуруПараметров()

Процедура ЗаменитьТег(Знач ИмяФайла, Знач ТегИсточник, Знач ТегЗамены)
	
	нТегИсточник = НРег(СокрЛП(ТегИсточник));
	Текст = ФайловыеОперации.ПрочитатьТекстФайла(ИмяФайла);
	Результат = "";
	ЗаменаВыполнена = Ложь;
	Для Индекс = 1 По СтрЧислоСтрок(Текст) Цикл
		Стр = СтрПолучитьСтроку(Текст, Индекс);
		Если НРег(СокрЛП(Стр)) = нТегИсточник Тогда
			Стр = ТегЗамены;
			ЗаменаВыполнена = Истина;
		КонецЕсли;
		Результат = Результат + Стр + Символы.ПС;
	КонецЦикла;

	Если ЗаменаВыполнена Тогда
		ФайловыеОперации.ЗаписатьТекстФайла(ИмяФайла, Результат);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции