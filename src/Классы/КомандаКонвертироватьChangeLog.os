#Область СлужебныйПрограммныйИнтерфейс

// Процедура - устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект описание команды
//
Процедура ОписаниеКоманды(Команда) Экспорт
	
	Команда.Опция("in i", "",
		"Исходный файл CHANGELOG.md в формате MarkDown из которого необходимо получить описание")
		.ТСтрока();

	Команда.Опция("out o", "",
		"Файл с новостью по обновлению из исходного файла в формате HTML или TXT в зависимости от расширения файла")
		.ТСтрока();

	Команда.Опция("format f", "html",
		"Формат генерации документации. Поддерживается txt, html, htmlfull")
		.ТПеречисление()
		.Перечисление("txt", "txt", "Документация в формате TXT")
		.Перечисление("html", "html", "Документация в кратком формате HTML. Все, что внутри тегов body")
		.Перечисление("htmlfull", "htmlfull", "Документация в полном формате HTML. Готовый HTML-файл со всеми стилями и с полным оформлением");

	Команда.Опция("version", "",
		"Из какой версии будет получено описание. Если не задано, то будет получено по последней (необязательный)")
		.ТСтрока();
		
КонецПроцедуры // ОписаниеКоманды()

// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	
	ЧтениеОпций = Новый ЧтениеОпцийКоманды(Команда);	
	ВыводОтладочнойИнформации = ЧтениеОпций.ЗначениеОпции("verbose");	
	ПараметрыСистемы.УстановитьРежимОтладки(ВыводОтладочнойИнформации);
	
	СтруктураПараметров = ПолучитьСтруктуруПараметров(Команда);
	РаботаСChangeLog.Конвертировать(СтруктураПараметров);
	
КонецПроцедуры // ВыполнитьКоманду()

Функция ПолучитьСтруктуруПараметров(Знач Команда)
	
	ПараметрыКоманды = Новый Структура;
	ПараметрыКоманды.Вставить("ИсходныйФайл", Команда.ЗначениеОпции("in"));
	Если НЕ ЗначениеЗаполнено(ПараметрыКоманды.ИсходныйФайл) Тогда
		ВызватьИсключение "Не заполнена опция ""-in"" - что конвертировать (md-файл)";
	КонецЕсли;
	ПараметрыКоманды.Вставить("ФайлРезультат", Команда.ЗначениеОпции("out"));
	Если НЕ ЗначениеЗаполнено(ПараметрыКоманды.ФайлРезультат) Тогда
		ВызватьИсключение "Не заполнена опция ""-out"" - куда сохранить";
	КонецЕсли;	
	ПараметрыКоманды.Вставить("Формат", Команда.ЗначениеОпции("format"));
	ПараметрыКоманды.Вставить("Версия", Команда.ЗначениеОпции("version"));
	
	Возврат ПараметрыКоманды;
	
КонецФункции

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиСобытий

// Процедура - обработчик события "ПриСозданииОбъекта"
//
// BSLLS:UnusedLocalMethod-off
Процедура ПриСозданииОбъекта()
	
КонецПроцедуры // ПриСозданииОбъекта()
// BSLLS:UnusedLocalMethod-on

#КонецОбласти // ОбработчикиСобытий