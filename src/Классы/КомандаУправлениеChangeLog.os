Перем мЭтоИнициализация;
Перем мЭтоИнформация;

// oscript cicd.os changelog-init -file c:\Temp\000_tempbase\1.md
// oscript cicd.os changelog -file c:\Temp\000_tempbase\1.md -outfile c:\Temp\000_tempbase\news.html -version 1.0.0.0
Процедура ЗарегистрироватьКоманду(Знач ИмяКоманды, Знач Парсер) Экспорт
	
	мЭтоИнициализация = НРег(ИмяКоманды) = "changelog-init";
	мЭтоИнформация = НРег(ИмяКоманды) = "changelog";
	
	Если мЭтоИнициализация Тогда
		ТекстКоманды = "Инициализация нового файла CHANGELOG.md для проекта";
	ИначеЕсли мЭтоИнформация Тогда
		ТекстКоманды = "Получение информации в формате HTML или TXT для определенной версии из файла CHANGELOG.md";
	Иначе
		ВызватьИсключение("Непредусмотренная команда");
	КонецЕсли;
	ОписаниеКоманды = Парсер.ОписаниеКоманды(ИмяКоманды, ТекстКоманды);
	
	Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды,
		"-file",
		"Исходный файл CHANGELOG.md в формате MarkDown из которого необходимо получить описание (обязательный)");
		
	Если мЭтоИнформация Тогда
		Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды,
			"-outfile",
			"Файл с новостью по обновлению из исходного файла в формате HTML или TXT в зависимости от расширения файла.
			|Если файл будет оканчиваться на html или htm, то новость сохранится в формате HTML.
			|Если же расширение будет другим, то новость сохранится в текстовом формате (обязательный)");
		Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды,
			"-version",
			"По какой версии будет получено описание обновления.
			|Если не задано, то будет получено по последней (необязательный)");
	КонецЕсли;
	
	Парсер.ДобавитьКоманду(ОписаниеКоманды);
	
КонецПроцедуры

Функция ВыполнитьКоманду(Знач Параметры) Экспорт
	
	Результат = МенеджерКомандПриложения.РезультатыКоманд().Успех;
	Лог = ПараметрыСистемы.ПолучитьЛог();
	
	Если мЭтоИнформация Тогда
		Результат = ПолучитьИнформацию(Параметры);
	ИначеЕсли мЭтоИнициализация Тогда
		Результат = СоздатьФайлChangeLog(Параметры);
	Иначе
		Лог.Ошибка("Неизвестное действие changelog");
		Возврат МенеджерКомандПриложения.РезультатыКоманд().НеверныеПараметры;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция СоздатьФайлChangeLog(Знач Параметры) Экспорт
	
	ВозможныйРезультат = МенеджерКомандПриложения.РезультатыКоманд();
	Лог = ПараметрыСистемы.ПолучитьЛог();
	
	Лог.Информация("Начало инициализации CHANGELOG.md");
	
	ФайлОсновной = Параметры["-file"];
	Если НЕ ЗначениеЗаполнено(ФайлОсновной) Тогда
		Лог.Ошибка("Не заполнен параметр ""-file"" - полный путь к Changelog.md!");
		Возврат ВозможныйРезультат.НеверныеПараметры;
	КонецЕсли;
	
	Версия = "1.0.0.0";
	ДатаВФормате = Формат(ТекущаяДата(), "ДФ=yyyy-MM-dd");
	Результат = СтрШаблон(
			"# Changelog
			|
			|Все изменения по проекту.
			|
			|## [%1] - %2
			|
			|### Внимание!
			|
			|* Важная заметка 1 **жирный** *курсив*
			|* Важная заметка 2 ~~Зачеркнутый~~
			|
			|### Добавлено
			|
			|* Добавлено дело 1 ***жирный курсив***
			|* Добавлено дело 2 `выделенный`
			|* Картинка ![Описание](https://github.com/images/modules/search/mona-love.png)
			|		
			|### Изменено
			|
			|* Изменение 1 __снова жиный__ _снова курсив_
			|* Изменение 2
			|
			|### Исправлено
			|
			|* Исправление ошибки", Версия, ДатаВФормате);
	
	ТД = Новый ТекстовыйДокумент;
	ТД.УстановитьТекст(Результат);
	ТД.Записать(ФайлОсновной);
	
	Лог.Информация("Окончание инициализации CHANGELOG.md");
	
	Возврат ВозможныйРезультат.Успех;
	
КонецФункции

Функция ПолучитьИнформацию(Знач Параметры) Экспорт
	
	ВозможныйРезультат = МенеджерКомандПриложения.РезультатыКоманд();
	Лог = ПараметрыСистемы.ПолучитьЛог();
	
	Лог.Информация("Начало получения новости ChangeLog");
	
	// Шаг 0. Проверка заполненных параметров
	ФайлОсновной = Параметры["-file"];
	Если НЕ ЗначениеЗаполнено(ФайлОсновной) Тогда
		Лог.Ошибка("Не заполнен параметр ""-file"" - полный путь к Changelog.md");
		Возврат ВозможныйРезультат.НеверныеПараметры;
	КонецЕсли;
	Если НЕ ФайловыеОперации.ФайлСуществует(ФайлОсновной) Тогда
		Лог.Ошибка("Параметр ""-file"" - содержит полный путь к не существующему файлу ChangeLog.md");
		Возврат ВозможныйРезультат.НеверныеПараметры;
	КонецЕсли;
	
	мФайлHTML = Параметры["-outfile"];
	Если НЕ ЗначениеЗаполнено(мФайлHTML) Тогда
		Лог.Ошибка("Не заполнен параметр ""-outfile"" - куда сохранить (где будет создан дистрибутив)!");
		Возврат ВозможныйРезультат.НеверныеПараметры;
	КонецЕсли;
	
	мВерсия = Параметры["-version"];
	Если НЕ ЗначениеЗаполнено(мВерсия) Тогда
		мВерсия = МаксимальнаяВерсияИзФайлаChangeLog(ФайлОсновной);
		Если НЕ ПустаяСтрока(мВерсия) Тогда
			ТекстИнформации = СтрШаблон("Определена максимальная версия ""%1"" в файле ChangeLog", мВерсия);
			Лог.Информация(ТекстИнформации);
		КонецЕсли;
	Иначе
		ТекстИнформации = СтрШаблон("Получение информации по версия ""%1"" из файла ChangeLog", мВерсия);
		Лог.Информация(ТекстИнформации);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(мВерсия) Тогда
		Лог.Ошибка("В файле ChangeLog не найдена область с версией. В виде ""## [1.0.0.0] - 2023-01-01""");
		Возврат ВозможныйРезультат.НеверныеПараметры;
	КонецЕсли;
	
	Обновление = Новый ТаблицаЗначений;
	Обновление.Колонки.Добавить("Раздел");
	Обновление.Колонки.Добавить("Текст");
	Обновление.Колонки.Добавить("ТекстHTML");
	Обновление.Колонки.Добавить("ЦветТекста");
	
	ТД = Новый ТекстовыйДокумент;
	Результат = "";
	Области = Новый ТекстовыйДокумент;
	Области.Прочитать(ФайлОсновной, КодировкаТекста.UTF8);
	ТекущаяВерсияФайла = "";
	
	Для а = 1 По Области.КоличествоСтрок() Цикл
		
		Текст = СокрЛП(Области.ПолучитьСтроку(а));
		Если ПустаяСтрока(Текст) Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрНайти(НРег(Текст), "# [" + НРег(мВерсия) + "]") > 0 Тогда
			ТекущаяВерсияФайла = мВерсия;
			Продолжить;
		Иначе
			Если СтрНачинаетсяС(Текст, "#") И СтрНайти(Текст, "# [") > 0 И СтрНайти(Текст, "]") > 0 Тогда
				Если НЕ ПустаяСтрока(ТекущаяВерсияФайла) Тогда
					Прервать;
				КонецЕсли;
			КонецЕсли;
			
			Если ПустаяСтрока(ТекущаяВерсияФайла) Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Результат = Результат + Текст + Символы.ПС;
		
	КонецЦикла;
	
	ТекстHTML = Результат;
	
	// header
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML, "###### (.*)", "<h6>$1</h6>");
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML, "##### (.*)", "<h5>$1</h5>");
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML, "#### (.*)", "<h4>$1</h4>");
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML, "### (.*)", "<h3>$1</h3>");
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML, "## (.*)", "<h2>$1</h2>");
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML, "# (.*)", "<h1>$1</h1>");
	
	// **жирный** __жирный__
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML, "(\*\*|__)(.*?)\1", "<strong>$2</strong>");
	// *курсив* _курсив_
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML, "(\*|_)(\S(.*?\S)?)(\*|_)", "<em>$2</em>");
	// `выделенный`
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML, "[`](.*?)[`]", "<code>$1</code>");
	// "выделенный"
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML, "\:\""(.*?)\""\:", "<q>$1</q>");
	// ~~Зачеркнутый~~
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML, "\~\~(.*?)\~\~", "<del>$1</del>");
	// Горизонтальные линии
	// --- *** ___ (три и более)
	СимволПС_ТегПрочерк = Символы.ПС + "<hr />";
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML, "[-]{3,}", СимволПС_ТегПрочерк);
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML, "[*]{3,}", СимволПС_ТегПрочерк);
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML, "[_]{3,}", СимволПС_ТегПрочерк);
	
	// Картинки
	// ![alt-текст](https://github.com/images/modules/search/mona-love.png "Текст заголовка логотипа 1")
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML,
			"\!\[([^\[]+)\]\(([^\""]+) \""([^\)]+)\""\)",
			"<img src=""$2"" alt=""$1"" title=""$3"">");
	
	// ![Это опциональный alt-текст](https://i.imgur.com/HzsGS7G.png)
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML,
			"\!\[([^\[]+)\]\(([^\)]+)\)",
			"<img src=""$2"" alt=""$1"">");
	
	// Это гиперссылка [here](myLib/README.md)
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML,
			"\[([^\[]+)\]\(([^\)]+)\)",
			"<a href=""$2"" target=""_blank"">$1</a>");
	
	// mails
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML,
			"([a-zA-Z0-9._-]+@[a-zA-Z0-9._-]+\.[a-zA-Z0-9_-]+)",
			"<a href=""mailto:$1"">$1</a>");
	
	// ul
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML,
			"\n\* (.*)",
			Символы.ПС + "<ul>" + Символы.ПС + "<li>$1</li>" + Символы.ПС + "</ul>");
	
	// ol
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML,
			"\n\- (.*)",
			Символы.ПС + "<ol>" + Символы.ПС + "<li>$1</li>" + Символы.ПС + "</ol>");
	
	// blockquote
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML,
			"\n(\>)(.*)",
			Символы.ПС + "<blockquote>$2</blockquote>");
	
	РегВыражение = Новый РегулярноеВыражение("^<\/?(ul|ol|li|h|p|bl)");
	РегВыражение.ИгнорироватьРегистр = Истина;
	РегВыражение.Многострочный = Истина;
	РезультатПреобразования = "";
	Для Индекс = 1 По СтрЧислоСтрок(ТекстHTML) Цикл
		Стр = СтрПолучитьСтроку(ТекстHTML, Индекс);
		Если РегВыражение.НайтиСовпадения(Стр).Количество() > 0 Тогда
			РезультатПреобразования = РезультатПреобразования + Стр + Символы.ПС;
		Иначе
			РезультатПреобразования = РезультатПреобразования + "<p>" + Стр + "</p>" + Символы.ПС;
		КонецЕсли;
	КонецЦикла;
	ТекстHTML = РезультатПреобразования;
	
	// Удаляем лишнее
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML, "\n<\/ul>\s?<ul>", "");
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML, "\n<\/ol>\s?<ol>", "");
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML, "<\/blockquote>\n<blockquote>", Символы.ПС);
	
	// Меняем для ul класс если найдено <h3 class=""updnew"">
	
	ТекстHTML = ЗаменитьЗаголовкиHTML(ТекстHTML, СинонимыAdded(), "updnew");
	ТекстHTML = ЗаменитьЗаголовкиHTML(ТекстHTML, СинонимыChanged(), "updedt");
	ТекстHTML = ЗаменитьЗаголовкиHTML(ТекстHTML, СинонимыDeprecated(), "updedt"); //<< ToDo
	ТекстHTML = ЗаменитьЗаголовкиHTML(ТекстHTML, СинонимыRemoved(), "updedt"); //<< ToDo
	ТекстHTML = ЗаменитьЗаголовкиHTML(ТекстHTML, СинонимыFixed(), "upderr");
	ТекстHTML = ЗаменитьЗаголовкиHTML(ТекстHTML, СинонимыSecurity(), "upderr"); //<< ToDo
	ТекстHTML = ЗаменитьЗаголовкиHTML(ТекстHTML, СинонимыAttention(), "updnws");
	
	МассивСинонимыAdded = МассивЗаголовков(СинонимыAdded());
	МассивСинонимыChanged = МассивЗаголовков(СинонимыChanged());
	МассивСинонимыDeprecated = МассивЗаголовков(СинонимыDeprecated());
	МассивСинонимыRemoved = МассивЗаголовков(СинонимыRemoved());
	МассивСинонимыFixed = МассивЗаголовков(СинонимыFixed());
	МассивСинонимыSecurity = МассивЗаголовков(СинонимыSecurity());
	МассивСинонимыAttention = МассивЗаголовков(СинонимыAttention());
	
	Текст = "";
	КлассCSS = "";
	Для Индекс = 1 По СтрЧислоСтрок(Результат) Цикл
		Стр = СокрЛП(СтрПолучитьСтроку(Результат, Индекс));
		Если ПустаяСтрока(Стр) Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрНачинаетсяС(Стр, "#") Тогда
			мСтр = Стр;
			Пока СтрНачинаетсяС(мСтр, "#") Цикл
				мСтр = Сред(мСтр, 2);
			КонецЦикла;
			мСтр = СокрЛП(мСтр);
			Если ПодстрокаВМассиве(МассивСинонимыAdded, мСтр) Тогда
				КлассCSS = "[+] ";
				Продолжить;
			ИначеЕсли ПодстрокаВМассиве(МассивСинонимыChanged, мСтр) Тогда
				КлассCSS = "[*] ";
				Продолжить;
			ИначеЕсли ПодстрокаВМассиве(МассивСинонимыFixed, мСтр) Тогда
				КлассCSS = "[-] ";
				Продолжить;
			ИначеЕсли ПодстрокаВМассиве(МассивСинонимыAttention, мСтр) Тогда
				КлассCSS = "[#] ";
				Продолжить;
			ИначеЕсли ПодстрокаВМассиве(МассивСинонимыDeprecated, мСтр) Тогда
				КлассCSS = "[D] ";
				Продолжить;
			ИначеЕсли ПодстрокаВМассиве(МассивСинонимыRemoved, мСтр) Тогда
				КлассCSS = "[R] ";
				Продолжить;
			ИначеЕсли ПодстрокаВМассиве(МассивСинонимыSecurity, мСтр) Тогда
				КлассCSS = "[S] ";
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если ПустаяСтрока(КлассCSS) Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрНачинаетсяС(Стр, "* ") ИЛИ СтрНачинаетсяС(Стр, "- ") Тогда
			Стр = Сред(Стр, 3);
		КонецЕсли;
		
		Стр = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(Стр, "(\*\*|__)(.*?)\1", "[b]$2[/b]");
		Стр = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(Стр, "(\*|_)(\S(.*?\S)?)(\*|_)", "[i]$2[/i]");
		Стр = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(Стр, "\~\~(.*?)\~\~", "[s]$1[/s]");
		Стр = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(Стр, "[`](.*?)[`]", "[code]$1[/code]");
		Стр = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(Стр, "\!\[([^\[]+)\]\(([^\""]+) \""([^\)]+)\""\)",
				"<img src=""$2"" alt=""$1"" title=""$3"">");
		Стр = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(Стр, "\!\[([^\[]+)\]\(([^\)]+)\)", "[img]$2[/img]");
		Стр = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(Стр, "\[([^\[]+)\]\(([^\)]+)\)", "[url=""$2""]$1[/url]");
		
		Текст = Текст + КлассCSS + Стр + Символы.ПС;
		
	КонецЦикла;
	
	ТД = Новый ТекстовыйДокумент;
	Если СтрНайти(НРег(мФайлHTML), ".html") > 0 ИЛИ СтрНайти(НРег(мФайлHTML), ".htm") > 0 Тогда
		ТекстНеОбработанный = ТекстHTML;
	Иначе
		ТекстНеОбработанный = Текст;
	КонецЕсли;
	ТД.УстановитьТекст(ТекстНеОбработанный);
	ТД.Записать(мФайлHTML);
	
	Лог.Информация("Окончание получения новости ChangeLog");
	
	Возврат ВозможныйРезультат.Успех;
	
КонецФункции

Функция СинонимыAdded()
	Возврат "Добавлено|Новый функционал|Added";
КонецФункции

Функция СинонимыChanged()
	Возврат "Изменения|Изменено|Changed";
КонецФункции

Функция СинонимыDeprecated()
	Возврат "Устарело|Deprecated";
КонецФункции

Функция СинонимыRemoved()
	Возврат "Удалено|Removed";
КонецФункции

Функция СинонимыFixed()
	Возврат "Исправление ошибок|Замечания к обновлению|Исправлено|Fixed";
КонецФункции

Функция СинонимыSecurity()
	Возврат "Безопасность|Security";
КонецФункции

Функция СинонимыAttention()
	Возврат "Внимание!|Новости|News|Attention!";
КонецФункции

Функция МассивЗаголовков(Знач Заголовки)
	Возврат СтроковыеФункции.РазложитьСтрокуВМассивПодстрок(НРег(Заголовки), "|", Ложь);
КонецФункции

Функция ПодстрокаВМассиве(Знач Массив, Знач Подстрока)
	
	Для Каждого Элемент Из Массив Цикл
		Если СтрНайти(НРег(Подстрока), НРег(Элемент)) > 0 Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
КонецФункции

Функция ЗаменитьЗаголовкиHTML(ТекстHTML, Заголовки, КлассCSS)
	
	ИсходныйШаблон = "\<h3\>(" + Заголовки + ")\<\/h3\>\n<ul>";
	ЗаменаШаблон = "<h3>$1</h3>" + Символы.ПС + "<ul class=""" + КлассCSS + """>";
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML,
			ИсходныйШаблон,
			ЗаменаШаблон);
	
	ИсходныйШаблон = "\<h2\>(" + Заголовки + ")\<\/h2\>\n<ul>";
	ЗаменаШаблон = "<h2>$1</h2>" + Символы.ПС + "<ul class=""" + КлассCSS + """>";
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML,
			ИсходныйШаблон,
			ЗаменаШаблон);
	
	ИсходныйШаблон = "\<h3\>(" + Заголовки + ")\<\/h3\>\n<ol>";
	ЗаменаШаблон = "<h3>$1</h3>" + Символы.ПС + "<ol class=""" + КлассCSS + """>";
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML,
			ИсходныйШаблон,
			ЗаменаШаблон);
	
	ИсходныйШаблон = "\<h2\>(" + Заголовки + ")\<\/h2\>\n<ol>";
	ЗаменаШаблон = "<h2>$1</h2>" + Символы.ПС + "<ol class=""" + КлассCSS + """>";
	ТекстHTML = ОбщегоНазначения.ЗаменитьПоРегулярномуВыражению(ТекстHTML,
			ИсходныйШаблон,
			ЗаменаШаблон);
	
	Возврат ТекстHTML;
	
КонецФункции

Функция МаксимальнаяВерсияИзФайлаChangeLog(ИмяФайла)
	
	мВерсия = "";
	
	РегВыражение = Новый РегулярноеВыражение("# \[(.*)\]");
	РегВыражение.ИгнорироватьРегистр = Истина;
	РегВыражение.Многострочный = Истина;
	Области = Новый ТекстовыйДокумент;
	Области.Прочитать(ИмяФайла, КодировкаТекста.UTF8);
	Текст = Области.ПолучитьТекст();
	Массив = РегВыражение.НайтиСовпадения(Текст);
	Индекс = 0;
	ТаблицаВерсий = Новый ТаблицаЗначений;
	ТаблицаВерсий.Колонки.Добавить("Версия");
	ТаблицаВерсий.Колонки.Добавить("ВесВерсии");
	Пока Индекс < Массив.Количество() Цикл
		Элемент = Массив[Индекс].Группы[1];
		НоваяСтрока = ТаблицаВерсий.Добавить();
		
		ВерсияСтрокой = Элемент.Значение;
		Вес = ОбщегоНазначения.ВесВерсии(ВерсияСтрокой);
		
		НоваяСтрока.Версия = ВерсияСтрокой;
		НоваяСтрока.ВесВерсии = Вес;
		
		Индекс = Индекс + 1;
	КонецЦикла;
	
	Если ТаблицаВерсий.Количество() > 0 Тогда
		ТаблицаВерсий.Сортировать("ВесВерсии Убыв");
		мВерсия = ТаблицаВерсий[0].Версия;
	КонецЕсли;
	
	Возврат мВерсия;
	
КонецФункции