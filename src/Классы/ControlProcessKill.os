#Область ПрограммныйИнтерфейс

// Процедура - устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект описание команды
//
Процедура ОписаниеКоманды(Команда) Экспорт
    
	Команда.Опция("pid p", "",
		"PID процесса, который необходимо завершить (не обязательный)")
		.ТСтрока();

	Команда.Опция("windowtitle t", "",
		"Регулярное выражение заголовка окна для завершения. Пример: ""Бухгалтерия"" (не обязательный)")
		.ТСтрока();

	Команда.Опция("processname p", "",
		"Заголовок окна или его маска, который необходимо завершить. Пример ""1cv8"" (не обязательный)")
		.ТСтрока();

КонецПроцедуры

// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт
    
	Параметры = ПолучитьСтруктуруПараметров(Команда);
	Если ЗначениеЗаполнено(Параметры.Идентификатор) Тогда
		ОбщегоНазначения.ЗавершитьПроцессПоИдентификатору(Параметры.Идентификатор);
	КонецЕсли;

	Если ЗначениеЗаполнено(Параметры.ЗаголовокОкна) Тогда
		ОбщегоНазначения.ЗавершитьПроцессПоЗаголовкуОкна(Параметры.ЗаголовокОкна);
	КонецЕсли;

	Если ЗначениеЗаполнено(Параметры.ИмяПроцесса) Тогда
		ОбщегоНазначения.ЗавершитьПроцессПоИмени(Параметры.ИмяПроцесса);
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСтруктуруПараметров(Знач Команда)

	ЧтениеОпций = Новый ЧтениеОпцийКоманды(Команда);	
	ВыводОтладочнойИнформации = ЧтениеОпций.ЗначениеОпции("verbose");	
	ПараметрыСистемы.УстановитьРежимОтладки(ВыводОтладочнойИнформации);

	ПараметрыКоманды = Новый Структура;
	ПараметрыКоманды.Вставить("Идентификатор", ЧтениеОпций.ЗначениеОпции("pid"));
	ПараметрыКоманды.Вставить("ЗаголовокОкна", ЧтениеОпций.ЗначениеОпции("windowtitle"));
	ПараметрыКоманды.Вставить("ИмяПроцесса", ЧтениеОпций.ЗначениеОпции("processname"));

	ЗаполненМинимумОдинПараметр = Ложь;
	Для Каждого КлючЗначение Из ПараметрыКоманды Цикл
		Если ЗначениеЗаполнено(КлючЗначение.Значение) Тогда
			ЗаполненМинимумОдинПараметр = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если НЕ ЗаполненМинимумОдинПараметр Тогда
		ОбщегоНазначения.ЗавершениеРаботыОшибка("%1 %2",
			"Необходимо заполнить хотя бы один параметр:",
			"--pid, --windowtitle, --processname");
	КонецЕсли;

	Возврат ПараметрыКоманды;

КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции