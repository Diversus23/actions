#Область ПрограммныйИнтерфейс

// Процедура - устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект описание команды
//
Процедура ОписаниеКоманды(Команда) Экспорт
		
	// Добавление дополнительных опций
	ПараметрыОпций = Работа1С.КонструкторЗагрузкиИБ();
	Работа1С.ОписаниеКоманды(Команда, ПараметрыОпций);
	
	Команда.Опция("file f", "",
		"Путь к dt-файлу для загрузки ИБ (обязательный)")
		.ТСтрока();
	
	Команда.Опция("del d", "",
		"Удалить исходный dt-файл после загрузки (не обязательный)")
		.ТБулево();

КонецПроцедуры // ОписаниеКоманды()

// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	
	СтруктураПараметров = ПолучитьСтруктуруПараметров(Команда);
	МенеджерКонфигуратора = Новый МенеджерКонфигуратора();
	МенеджерКонфигуратора.Инициализация(СтруктураПараметров);
	МенеджерКонфигуратора.ЗагрузитьИБ(СтруктураПараметров);
	МенеджерКонфигуратора.Деструктор();
	
КонецПроцедуры // ВыполнитьКоманду()

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСтруктуруПараметров(Знач Команда)
	
	ЧтениеОпций = Новый ЧтениеОпцийКоманды(Команда);
	ВыводОтладочнойИнформации = ЧтениеОпций.ЗначениеОпции("verbose");
	ПараметрыСистемы.УстановитьРежимОтладки(ВыводОтладочнойИнформации);
	
	ПараметрыОпций = Работа1С.КонструкторЗагрузкиИБ();

	ПараметрыКоманды = Работа1С.ПрочитатьПараметры(Команда, ПараметрыОпций);	
	ПараметрыКоманды.Вставить("ПутьКФайлу", ЧтениеОпций.ЗначениеОпции("file", Истина));
	ПараметрыКоманды.Вставить("УдалитьПослеЗагрузки", ЧтениеОпций.ЗначениеОпции("del", Ложь));

	Если НЕ ФайловыеОперации.ФайлСуществует(ПараметрыКоманды.ПутьКФайлу) Тогда
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Dt-файл <%1> не найден. Загрузка невозможна",
			ПараметрыКоманды.ПутьКФайлу);
	КонецЕсли;
	
	Возврат ПараметрыКоманды;
	
КонецФункции // ПолучитьСтруктуруПараметров()

#КонецОбласти // СлужебныеПроцедурыИФункции