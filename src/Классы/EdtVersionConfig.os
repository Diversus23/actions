#Область ПрограммныйИнтерфейс

// Процедура - устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект описание команды
//
Процедура ОписаниеКоманды(Команда) Экспорт
		
	Команда.Опция("project p", "",
		"Каталог с файлами конфигурации в формате EDT (обязательный)")
		.ТСтрока();
	
	Команда.Опция("parts", "",
		"Из скольки частей состоит версия. Пример: ""1.0.0.0"" - 4, ""1.0.0"" - 3. По умолчанию 4 (не обязательный)")
		.ТЧисло();
		
КонецПроцедуры // ОписаниеКоманды()

// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	
	ПараметрыКоманды = ПолучитьСтруктуруПараметров(Команда);
	Версия = РаботаEDT.ВерсияКонфигурацииИзПроекта(ПараметрыКоманды.КаталогПроекта, ПараметрыКоманды.ЧастейВВерсии);
	Если ПустаяСтрока(Версия) Тогда		
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Версия в файле конфигурации Configuration.mdo не найдена");
	КонецЕсли;
	Сообщить(Версия);
	
КонецПроцедуры // ВыполнитьКоманду()

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСтруктуруПараметров(Знач Команда)
	
	ЧтениеОпций = Новый ЧтениеОпцийКоманды(Команда);
	ВыводОтладочнойИнформации = ЧтениеОпций.ЗначениеОпции("verbose");
	ПараметрыСистемы.УстановитьРежимОтладки(ВыводОтладочнойИнформации);
	
	ПараметрыКоманды = Новый Структура;
	ПараметрыКоманды.Вставить("КаталогПроекта", ЧтениеОпций.ЗначениеОпции("project", Истина));
	ПараметрыКоманды.Вставить("ЧастейВВерсии", ЧтениеОпций.ЗначениеОпции("parts", Ложь, 4));
	
	ТекстОшибки = "Каталог ""%1"" (%2) не существует";
	Если НЕ ФайловыеОперации.КаталогСуществует(ПараметрыКоманды.КаталогПроекта) Тогда
		ОбщегоНазначения.ЗавершениеРаботыОшибка(ТекстОшибки, "--project", ПараметрыКоманды.КаталогПроекта);
	КонецЕсли;

	Возврат ПараметрыКоманды;
	
КонецФункции // ПолучитьСтруктуруПараметров()

#КонецОбласти // СлужебныеПроцедурыИФункции