#Область ПрограммныйИнтерфейс

// Процедура - устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект описание команды
//
Процедура ОписаниеКоманды(Команда) Экспорт
    
	Команда.Опция("file f", "",
		"Полное имя файла создаваемого zip-архива (обязательный)")
		.ТСтрока();

	Команда.Опция("mask m", "",
		"Файлы и папки, включаемые в архив. Можно использовать разделитель "";"" и маски файлов (обязательный).
		| Примеры:
		|   ""C:\Path\File1.txt|C:\Path\Folder""
		|   ""C:\Path\*.txt;C:\Path\Folder\*.*""
		|   ""C:\Path\Folder""")
		.ТСтрока();		

КонецПроцедуры // ОписаниеКоманды()

// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	
	СтруктураПараметров = ПолучитьСтруктуруПараметров(Команда);

	МаскаДобавляемыхФайлов = СтруктураПараметров.Маска;
	РазделительФайлов = ";";
	МассивФайловИПапок = СтроковыеОперации.РазложитьСтрокуВМассивПодстрок(МаскаДобавляемыхФайлов,
			РазделительФайлов, Истина, Истина);
	Если МассивФайловИПапок.Количество() = 0 Тогда
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Параметр ""%1"" не содержит файлов и директорий", "--mask");
	КонецЕсли;
	Архиватор = Новый Архиватор(СтруктураПараметров.ИмяФайла);
	Архиватор.Добавить(МассивФайловИПапок);
    
КонецПроцедуры // ВыполнитьКоманду()

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСтруктуруПараметров(Знач Команда)

	ЧтениеОпций = Новый ЧтениеОпцийКоманды(Команда);	
	ВыводОтладочнойИнформации = ЧтениеОпций.ЗначениеОпции("verbose");	
	ПараметрыСистемы.УстановитьРежимОтладки(ВыводОтладочнойИнформации);

	ПараметрыКоманды = Новый Структура;
	ПараметрыКоманды.Вставить("ИмяФайла", ЧтениеОпций.ЗначениеОпции("file", Истина));
	ПараметрыКоманды.Вставить("Маска", ЧтениеОпций.ЗначениеОпции("mask", Истина));
	Возврат ПараметрыКоманды; 

КонецФункции // ПолучитьСтруктуруПараметров()

#КонецОбласти // СлужебныеПроцедурыИФункции