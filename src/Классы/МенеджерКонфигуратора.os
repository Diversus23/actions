#Использовать v8runner
#Использовать asserts
#Использовать v8storage

// BSLLS:NumberOfOptionalParams-off

// Лог модуля
Перем Лог;
// Каталог под временную ИБ, если она нужна
Перем КаталогВременнойИБ;
// Конфигуратор 1С
Перем Конфигуратор;
// Инфомрация о метаданных (версия, поставщик и т.д.)
Перем ИнформацияОМетаданных;

#Область ПрограммныйИнтерфейс

// Доступ напрямую к управлению конфигрутору.
//
// Возвращаемое значение:
//	УправлениеКонфигуратором - конфигуратор.
//
Функция Конфигуратор() Экспорт

	Возврат Конфигуратор;
	
КонецФункции

// Инициализирует МенеджерКонфигуратора. Надо вызывать всегда после создания объекта
//
// Параметры:
//  Параметры    - Структура     - настройки запуска конфигуратора
//
Процедура Инициализация(Знач Параметры) Экспорт
	
	ТекущаяПроцедура = "Инициализация";
	Ожидаем.Что(Параметры.СтрокаПодключения, ТекущаяПроцедура + ": не задана строка подключения").Заполнено();
	
	Конфигуратор = Новый УправлениеКонфигуратором();
	ИнформацияОМетаданных = Неопределено;
	
	КаталогВременнойИБ = ВременныеФайлы.СоздатьКаталог();
	Конфигуратор.КаталогСборки(КаталогВременнойИБ);
	
	Конфигуратор.УстановитьКонтекст(Параметры.СтрокаПодключения, Параметры.Пользователь, Параметры.Пароль);

	Если Параметры.Свойство("ВерсияПлатформы") И ЗначениеЗаполнено(Параметры.ВерсияПлатформы) Тогда
		Конфигуратор.ИспользоватьВерсиюПлатформы(Параметры.ВерсияПлатформы);
	КонецЕсли;
	
	Если Параметры.Свойство("КлючРазрешенияЗапуска") И ЗначениеЗаполнено(Параметры.КлючРазрешенияЗапуска) Тогда
		Конфигуратор.УстановитьКлючРазрешенияЗапуска(Параметры.КлючРазрешенияЗапуска);
	КонецЕсли;
	
	Если Параметры.Свойство("КодЯзыка") И ЗначениеЗаполнено(Параметры.КодЯзыка) Тогда
		Конфигуратор.УстановитьКодЯзыка(Параметры.КодЯзыка);
	КонецЕсли;
	
	Если Параметры.Свойство("КодЯзыкаСеанса") И ЗначениеЗаполнено(Параметры.КодЯзыкаСеанса) Тогда
		Конфигуратор.УстановитьКодЯзыкаСеанса(Параметры.КодЯзыкаСеанса);
	КонецЕсли;
	
КонецПроцедуры

// Уничтожает МенеджерКонфигуратора. Надо вызывать всегда после того, как объекта перестал быть нужен.
//
Процедура Деструктор() Экспорт
	Попытка
		Если КаталогВременнойИБ <> Неопределено Тогда
			ВременныеФайлы.УдалитьФайл(КаталогВременнойИБ);
		КонецЕсли;
	Исключение
		Лог.Отладка(ОписаниеОшибки());
	КонецПопытки;
	
	КаталогВременнойИБ = Неопределено;
КонецПроцедуры

// Создает файловую базу 1С.
//
// Параметры:
//  Параметры    - Структура     - настройки запуска конфигуратора
//
Процедура СоздатьФайловуюИБ(Знач Параметры) Экспорт
	
	Лог.Информация("Создание файловой ИБ");
	Лог.Информация("Каталог %1", Параметры.КаталогБазы);
	
	Попытка
		Конфигуратор.СоздатьФайловуюБазу(Параметры.КаталогБазы,
			Параметры.ПутьКШаблону,
			Параметры.ИмяБазыВСписке);
		Лог.Информация("Создание файловой ИБ завершено");
	Исключение
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Произошла ошибка при создании файловой ИБ %1",
			ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

// Создает серверную базу 1С.
//
// Параметры:
//  Параметры    - Структура     - настройки запуска конфигуратора
//
// BSLLS:CyclomaticComplexity-off
// BSLLS:CognitiveComplexity-off
Процедура СоздатьСервернуюИБ(Знач Параметры) Экспорт

	Лог.Информация("Создание серверной ИБ");

	// Заполнение параметров базы 1С
	ПараметрыБазы1С = Новый Структура();
	ПараметрыБазы1С.Вставить("Сервер1С", Параметры.Сервер1С);
	Лог.Информация("Сервер 1С: %1", Параметры.Сервер1С);
	ПараметрыБазы1С.Вставить("ИмяИБ", Параметры.ИмяИБ);
	Лог.Информация("Имя базы 1С: %1", Параметры.ИмяИБ);
	Если ЗначениеЗаполнено(Параметры.РазрешитьРегЗадания) Тогда
		ПараметрыБазы1С.Вставить("РазрешитьРегЗадания", Параметры.РазрешитьРегЗадания);
	Иначе
		ПараметрыБазы1С.Вставить("РазрешитьРегЗадания", Истина);
	КонецЕсли;
	Если ЗначениеЗаполнено(Параметры.РазрешитьРегЗадания) Тогда
		ПараметрыБазы1С.Вставить("РазрешитьВыдачуЛицензий", Параметры.РазрешитьВыдачуЛицензий);
	Иначе
		ПараметрыБазы1С.Вставить("РазрешитьВыдачуЛицензий", Ложь);
	КонецЕсли;
	
	// Параметры СУБД
	ПараметрыСУБД = Новый Структура();

	Если ЗначениеЗаполнено(Параметры.ТипСУБД) Тогда
		ПараметрыСУБД.Вставить("ТипСУБД", Параметры.ТипСУБД);
	Иначе
		ПараметрыСУБД.Вставить("ТипСУБД", Перечисления.ТипыСУБД.MSSQLServer);
	КонецЕсли;

	Если ЗначениеЗаполнено(Параметры.СерверСУБД) Тогда
		ПараметрыСУБД.Вставить("СерверСУБД", Параметры.СерверСУБД);	
	Иначе
		ПараметрыСУБД.Вставить("СерверСУБД", Параметры.Сервер1С);	
	КонецЕсли;
	Лог.Информация("Сервер БД %1:%2", ПараметрыСУБД.ТипСУБД, ПараметрыСУБД.СерверСУБД);
	
	Если ЗначениеЗаполнено(Параметры.ПользовательСУБД) Тогда
		ПараметрыСУБД.Вставить("ПользовательСУБД", Параметры.ПользовательСУБД);
		Лог.Информация("ПользовательСУБД: %1", ПараметрыСУБД.ПользовательСУБД);
	Иначе
		ПараметрыСУБД.Вставить("ПользовательСУБД", "");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ПарольСУБД) Тогда
		ПараметрыСУБД.Вставить("ПарольСУБД", Параметры.ПарольСУБД);
		Лог.Информация("ПарольСУБД: ***");
	Иначе
		ПараметрыСУБД.Вставить("ПарольСУБД", "");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ИмяБД) Тогда
		ПараметрыСУБД.Вставить("ИмяБД", Параметры.ИмяБД);
	Иначе
		ПараметрыСУБД.Вставить("ИмяБД", ПараметрыБазы1С.ИмяИБ);
	КонецЕсли;
	Лог.Информация("Имя базы БД: %1", ПараметрыСУБД.ИмяБД);

	Если ЗначениеЗаполнено(Параметры.СмещениеДат) Тогда
		ПараметрыСУБД.Вставить("СмещениеДат", Параметры.СмещениеДат);
	Иначе
		ПараметрыСУБД.Вставить("СмещениеДат", 2000);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.СоздаватьБД) Тогда
		ПараметрыСУБД.Вставить("СоздаватьБД", Параметры.СоздаватьБД);
	Иначе
		ПараметрыСУБД.Вставить("СоздаватьБД", Истина);
	КонецЕсли;
	
	АвторизацияВКластере = Новый Структура();
	Если ЗначениеЗаполнено(Параметры.АдминистраторКластера) Тогда
		АвторизацияВКластере.Вставить("Имя", Параметры.АдминистраторКластера);
		Лог.Информация("Администратор кластера: %1", АвторизацияВКластере.Имя);
	Иначе
		АвторизацияВКластере.Вставить("Имя", "");
	КонецЕсли;
	Если ЗначениеЗаполнено(Параметры.ПарольКластера) Тогда
		АвторизацияВКластере.Вставить("Пароль", Параметры.ПарольКластера);
		Лог.Информация("Пароль администратора кластера: ***");
	Иначе
		АвторизацияВКластере.Вставить("Пароль", "");
	КонецЕсли;
	ОшибкаЕслиСуществует = Истина;
	
	ПутьКШаблону = "";
	Если ЗначениеЗаполнено(Параметры.ПутьКШаблону) Тогда
		ПутьКШаблону = Параметры.ПутьКШаблону;
		Лог.Информация("Шаблон создаваемой ИБ: %1", ПутьКШаблону);
	КонецЕсли;
	
	ИмяБазыВСписке = "";
	Если ЗначениеЗаполнено(Параметры.ИмяБазыВСписке) Тогда
		ИмяБазыВСписке = Параметры.ИмяБазыВСписке;
		Лог.Информация("Имя в списке баз пользователя: %1", ИмяБазыВСписке);
	КонецЕсли;

	Попытка

		Конфигуратор.СоздатьСервернуюБазу(ПараметрыБазы1С,
			ПараметрыСУБД,
			АвторизацияВКластере,
			ОшибкаЕслиСуществует,
			ПутьКШаблону,
			ИмяБазыВСписке);

		Лог.Информация("Создание ИБ завершено");

	Исключение

		ОбщегоНазначения.ЗавершениеРаботыОшибка("Произошла ошибка при создании ИБ на сервере %1",
			Конфигуратор.ВыводКоманды());
			
	КонецПопытки;	
	
КонецПроцедуры
// BSLLS:CognitiveComplexity-on
// BSLLS:CyclomaticComplexity-on

// Выгрузить информационную базу 1С в DT-файл.
//
// Параметры:
//  Параметры    - Структура     - настройки для запуска
//
Процедура ВыгрузитьИБ(Знач Параметры) Экспорт
	
	Лог.Информация("Начало выгрузки информационной базы 1С");
	Попытка
		Лог.Информация("Выгрузка в файл <%1>", Параметры.ПутьКФайлу);
		Конфигуратор.ВыгрузитьИнформационнуюБазу(Параметры.ПутьКФайлу);
		Лог.Информация("Выгрузка завершена");
	Исключение
		ТекстОшибки = ОписаниеОшибки();
		Вывод = Конфигуратор.ВыводКоманды();
		Если НЕ ПустаяСтрока(Вывод) Тогда
			ТекстОшибки = СтрШаблон("%1. %2", ТекстОшибки, Вывод);
		КонецЕсли;
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Произошла ошибка при выгрузке ИБ: %1", ТекстОшибки);
	КонецПопытки;
	
КонецПроцедуры

// Загрузить информационную базу 1С из DT-файл.
//
// Параметры:
//  Параметры    - Структура     - настройки для запуска
//
Процедура ЗагрузитьИБ(Знач Параметры) Экспорт
	
	Лог.Информация("Начало загрузки информационной базы 1С");	
	Попытка
		Лог.Информация("Загрузка из файла <%1>", Параметры.ПутьКФайлу);
		Конфигуратор.ЗагрузитьИнформационнуюБазу(Параметры.ПутьКФайлу);
		Если Параметры.УдалитьПослеЗагрузки = Истина Тогда
			УдалитьФайлы(Параметры.ПутьКФайлу);
			Лог.Информация("Исходный dt-файл %1 удален", Параметры.ПутьКФайлу);
		КонецЕсли;
		Лог.Информация("Загрузка завершена");
	Исключение
		ТекстОшибки = ОписаниеОшибки();
		Вывод = Конфигуратор.ВыводКоманды();
		Если НЕ ПустаяСтрока(Вывод) Тогда
			ТекстОшибки = СтрШаблон("%1. %2", ТекстОшибки, Вывод);
		КонецЕсли;
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Произошла ошибка при загрузке ИБ: %1", ТекстОшибки);
	КонецПопытки;

КонецПроцедуры

// BSLLS:NumberOfOptionalParams-on

// Возвращает имя базы для серверной ИБ, последняя папка для файловой ИБ.
//
// Возвращаемое значение:
//	Строка - имя базы.
//
Функция ИмяИБ() Экспорт
	
	СимволовОтНачала = 3;
	Контекст = Конфигуратор.ПолучитьКонтекст();
	КлючСоединенияСБазой = СокрЛП(Контекст.КлючСоединенияСБазой);
	ТекИмяБазы = "";
	Если СтрНачинаетсяС(ВРег(КлючСоединенияСБазой), "/F") Тогда
		ТекИмяБазы = Сред(КлючСоединенияСБазой, СимволовОтНачала);
		ТекИмяБазы = ФайловыеОперации.УдалитьПоследнийРазделительПути(ТекИмяБазы);
		НомерСлеша = 0;		
		Для Сч = 1 По СтрДлина(ТекИмяБазы) Цикл
			Если Сред(ТекИмяБазы, Сч, 1) = "\" Тогда
				НомерСлеша = Сч;
			КонецЕсли;
		КонецЦикла;
		ТекИмяБазы = Сред(ТекИмяБазы, НомерСлеша + 1);
	ИначеЕсли СтрНачинаетсяС(ВРег(КлючСоединенияСБазой), "/S") Тогда
		ТекИмяБазы = Сред(КлючСоединенияСБазой, СимволовОтНачала);
	Иначе
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Не известен контекст выполнения %1", КлючСоединенияСБазой);
	КонецЕсли;
		
	Возврат СокрЛП(ТекИмяБазы);

КонецФункции

// Возвращает имя файла, построенном по принципу: Префикс (имя базы) + ДатаВремя + Расширение
//
// Параметры:
//	Префикс - Строка - префикс имени. Если не задано, то будет определено по имени базы.
//	ФорматДаты - Строка - формат даты.
//	Расширение - Строка - расширение для файла.
//	
// Возвращаемое значение:
//	Строка - имя файла.
//
// Пример: Base_2017_04_28_19_02_12.dt
Функция ИмяФайлаПоИмениИБНаДату(Знач Префикс = "", Знач ФорматДаты = "yyyy_MM_dd_ЧЧ_мм_сс",
	Знач Расширение = "dt") Экспорт

	Результат = "";

	Если НЕ ПустаяСтрока(Префикс) Тогда
		Результат = Префикс + "_";
	Иначе
		ИмяБазы = ИмяИБ();
		Если НЕ ПустаяСтрока(ИмяБазы) Тогда
			Результат = ИмяБазы + "_";
		КонецЕсли;
	КонецЕсли;

	Результат = Результат + Формат(ТекущаяДата(), "ДФ=" + ФорматДаты) + "." + Расширение;

	Возврат Результат;

КонецФункции

// Осуществляет проверку конфигурации
//
// Параметры:
//	ПараметрыПроверки - Структура - ключи проверки конфигуации.
//
Процедура ВыполнитьПроверкуКонфигурации(Знач ПараметрыПроверки) Экспорт

	Лог.Информация("Выполнение проверки конфигурации");

	Если ПараметрыПроверки.Количество() = 0 Тогда
		ПараметрыПроверки = Работа1С.ПараметрыПроверкиКонфигурацииПоУмолчанию();
		Лог.Информация("Параметры проверки не переданы, используем ключи по умолчанию");
	КонецЕсли;

	КлючиПроверки = Работа1С.КлючиПроверкиКонфигурации();
	Лог.Информация("> Проверка с ключами:");
	Для Каждого КлючЗначение Из ПараметрыПроверки Цикл
		Лог.Информация(">>> %1 - %2", КлючЗначение.Ключ, КлючиПроверки[КлючЗначение.Ключ]);
	КонецЦикла;
	
	Попытка
		Конфигуратор.ВыполнитьРасширеннуюПроверкуКонфигуратора(ПараметрыПроверки);
	Исключение		
		ТекстОшибки = ОписаниеОшибки();
		Вывод = Конфигуратор.ВыводКоманды();
		Если НЕ ПустаяСтрока(Вывод) Тогда
			ТекстОшибки = СтрШаблон("%1. %2", ТекстОшибки, Вывод);
		КонецЕсли;
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Проверка завершена с ошибками: %1", ТекстОшибки);		
	КонецПопытки;
	
	Лог.Информация("Проверка завершена");

КонецПроцедуры

// Выгружает файл конфигурации из ИБ
//
// Параметры:
//  ИмяФайла - Строка - Путь к результату - выгружаемому файлу конфигурации (*.cf)
//
Процедура ВыгрузитьКонфигурациюВФайл(Знач ИмяФайла) Экспорт

	Лог.Информация("Выгрузка конфигурации в cf-файл");

	Если Не ЗначениеЗаполнено(ИмяФайла) Тогда
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Не указан имя cf-файла конфигурации");
	КонецЕсли;
	Файл = Новый Файл(ИмяФайла);
	ФС.ОбеспечитьКаталог(Файл.Путь);

	Попытка
		Конфигуратор.ВыгрузитьКонфигурациюВФайл(ИмяФайла);
	Исключение
		ТекстОшибки = ОписаниеОшибки();
		Вывод = Конфигуратор.ВыводКоманды();
		Если НЕ ПустаяСтрока(Вывод) Тогда
			ТекстОшибки = СтрШаблон("%1. %2", ТекстОшибки, Вывод);
		КонецЕсли;
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Выгрузка конфигурации завершилась с ошибками: %1", ТекстОшибки);		
	КонецПопытки;

	Лог.Информация("Выгрузка в cf-файл завершена");

КонецПроцедуры

// Загружает файл конфигурации в текущую базу данных.
//	Параметры:
//		ПутьКФайлу - Строка - Путь к файлу *.cf
//		СниматьСПоддержки - Булево - снимает с поддержки, отключает от хранилища, по умолчанию Истина
//
Процедура ЗагрузитьКонфигурациюИзФайла(Знач ПутьКФайлу, Знач СниматьСПоддержки = Истина) Экспорт
	Перем ФайлЗагрузки, ПараметрыЗапуска;

	Лог.Информация("Загружаем файл конфигурации %1", ПутьКФайлу);
	ФайлЗагрузки = Новый Файл(ПутьКФайлу);
	Ожидаем.Что(ФайлЗагрузки.Существует(), "Путь к файлу загрузки не существует " + ФайлЗагрузки.ПолноеИмя).ЕстьИстина();

	Если ТипЗнч(СниматьСПоддержки) = Тип("Булево") И СниматьСПоддержки Тогда
		ПараметрыЗапуска = Конфигуратор.ПолучитьПараметрыЗапуска();
		ПараметрыЗапуска.Добавить("/ConfigurationRepositoryUnbindCfg -force");
		Конфигуратор.ВыполнитьКоманду(ПараметрыЗапуска);

		ПараметрыЗапуска = Конфигуратор.ПолучитьПараметрыЗапуска();
		ПараметрыЗапуска.Добавить("/ManageCfgSupport -disableSupport -force");
		Конфигуратор.ВыполнитьКоманду(ПараметрыЗапуска);
	КонецЕсли;

	Попытка
		Конфигуратор.ЗагрузитьКонфигурациюИзФайла(ФайлЗагрузки.ПолноеИмя);
	Исключение
		ТекстОшибки = ОписаниеОшибки();
		Вывод = Конфигуратор.ВыводКоманды();
		Если НЕ ПустаяСтрока(Вывод) Тогда
			ТекстОшибки = СтрШаблон("%1. %2", ТекстОшибки, Вывод);
		КонецЕсли;
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Загрузка конфигурации завершилась с ошибками: %1", ТекстОшибки);		
	КонецПопытки;	

	Лог.Информация("Загрузка конфигурации из cf-файла успешно завершена");

КонецПроцедуры

// Выгружает файл расширения из ИБ
//
// Параметры:
//  ПутьКФайлу - Строка - Путь к результату - выгружаемому файлу конфигурации (*.cfe)
//  ИмяРасширения - Строка - Имя расширения
//
Процедура ВыгрузитьРасширениеВФайл(Знач ПутьКФайлу, Знач ИмяРасширения) Экспорт

	Лог.Информация("Запускаю выгрузку расширения %1 в файл <%2>", ИмяРасширения, ПутьКФайлу);

	Попытка
		ПараметрыЗапуска = Конфигуратор.ПолучитьПараметрыЗапуска();
		ПараметрыЗапуска.Добавить("/DumpCfg");
		ПараметрыЗапуска.Добавить("""" + ФайловыеОперации.АбсолютныйПуть(ПутьКФайлу) + """");
		ПараметрыЗапуска.Добавить("-Extension """ + ИмяРасширения + """");
		Конфигуратор.ВыполнитьКоманду(ПараметрыЗапуска);

		Текст = Конфигуратор.ВыводКоманды();
		Если Не ПустаяСтрока(Текст) Тогда
			Лог.Информация(Текст);
		КонецЕсли;
	Исключение
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Ошибка выгрузки расширения в файл %1 (%2)", 
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
			Конфигуратор.ВыводКоманды());
	КонецПопытки;

	Лог.Информация("Выгрузка расширения в файл завершена");

КонецПроцедуры

// Загружает файл расширения в текущую базу данных.
//	Параметры:
//		ПутьКФайлу - Строка - Путь к файлу *.cfe
//		ИмяРасширения - Строка - имя расширения
//		ОбновитьКонфигурациюИБ - Строка - обновить конфигуарцию ИБ после загрузки расширения
//
Процедура ЗагрузитьФайлРасширения(Знач ПутьКФайлу, Знач ИмяРасширения, Знач ОбновитьКонфигурациюИБ = Ложь) Экспорт

	Лог.Информация("Загружаю файл расширения <%1>", ПутьКФайлу);

	Файл = Новый Файл(ПутьКФайлу);
	Ожидаем.Что(Файл.Существует(), "Путь к файлу загрузки не существует " + Файл.ПолноеИмя).ЕстьИстина();
	Конфигуратор.ЗагрузитьРасширениеИзФайла(Файл.ПолноеИмя, ИмяРасширения, ОбновитьКонфигурациюИБ);

	Лог.Информация("Загрузка расширения %1 из cfe-файла успешно завершена", ИмяРасширения);

КонецПроцедуры

// Выводит информацию о всех установленных расширениях в конфигурации
//
Процедура ПоказатьСписокВсехРасширенийКонфигурации() Экспорт
	СписокВсехРасширений = ПолучитьСписокВсехРасширений();
	Лог.Информация("Список расширений конфигурации:
				   |%1", СписокВсехРасширений);
КонецПроцедуры

// Получает информацию о всех установленных расширениях в конфигурации
//
// Возвращаемое значение:
//	Строка - список всех расширений в конфигурации.
Функция ПолучитьСписокВсехРасширений() Экспорт

	ПараметрыЗапуска = Конфигуратор.ПолучитьПараметрыЗапуска();

	ПараметрыЗапуска.Добавить("/Visible");
	ПараметрыЗапуска.Добавить("/DumpDBCfgList");
	ПараметрыЗапуска.Добавить("-AllExtensions");
	Конфигуратор.ВыполнитьКоманду(ПараметрыЗапуска);

	Возврат Конфигуратор.ВыводКоманды();

КонецФункции

// Собирает из исходников конфигурацию
//
// Параметры:
//	Каталог - Строка - путь каталогу с иходниками расширения
//
Процедура ЗагрузитьКонфигурациюИзФайловXML(Знач Каталог) Экспорт

	Лог.Информация("Выполняю сборку/загрузку конфигурации из каталога <%1>", Каталог);

	ПараметрыЗапуска = Конфигуратор.ПолучитьПараметрыЗапуска();
	ПараметрыЗапуска.Добавить("/Visible");
	ПараметрыЗапуска.Добавить("/LoadConfigFromFiles """ + Каталог + """");
	ПараметрыЗапуска.Добавить("/UpdateDBCfg");

	Попытка
		Конфигуратор.ВыполнитьКоманду(ПараметрыЗапуска);			
	Исключение
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Произошла ошибка при загрузке конфигурации из файлов %1 %2",
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), Конфигуратор.ВыводКоманды());
	КонецПопытки;

	Лог.Информация("Загрузка конфигурации завершена");

КонецПроцедуры

// Выгружает конфигурацию в исходники XML
//
// Параметры:
//	КаталогВыгрузки - Строка - путь каталогу с иходниками расширения
//  ФайлВерсии - Строка - Путь к файлу версии
//  ТолькоИзмененные - Булево - Выгружать только измененные файлы для ускорения выгрузки
//  ИспользоватьПереименования - Булево - Переименовывать файлы в имена по папкам согласно иерархии метаданных
//
Процедура ВыгрузитьКонфигурациюВФайлыXML(КаталогВыгрузки, Знач ФайлВерсии = "",
	Знач ТолькоИзмененные = Ложь,
	Знач ИспользоватьПереименования = Ложь) Экспорт

	Лог.Информация("Выполняю выгрузку конфигурации в каталог XML <%1>", КаталогВыгрузки);
	КаталогРаспаковки = ?(ИспользоватьПереименования, ВременныеФайлы.СоздатьКаталог(), КаталогВыгрузки);

	ФайлОбъект = Новый Файл(ФайлВерсии);
	ТолькоИзмененные = (ТолькоИзмененные И ФайлОбъект.Существует());
	ИспользоватьПереименования = ?(ТолькоИзмененные, Ложь, ИспользоватьПереименования);
	Попытка
		Конфигуратор.ВыгрузитьКонфигурациюВФайлы(КаталогРаспаковки, , ТолькоИзмененные, ФайлВерсии);
	Исключение
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Произошла ошибка при выгрузке конфигурации в каталог XML %1 %2",
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), Конфигуратор.ВыводКоманды());
	КонецПопытки;

	Лог.Информация("Выгрузка в исходники завершена.");

КонецПроцедуры

// Собирает из исходников расширение с указанным именем
//
// Параметры:
//	Каталог - Строка - путь каталогу с иходниками расширения
// 	ИмяРасширения - Строка - Имя расширения
//	ОбновитьКонфигурациюИБ - Булево - Признак обновления расширения в базе, 
//									  имеет смысл только на пустой базе или при первой загрузке.
//
Процедура СобратьИзИсходниковРасширение(Знач Каталог, Знач ИмяРасширения, Знач ОбновитьКонфигурациюИБ = Ложь) Экспорт

	Лог.Информация("Выполняю сборку/загрузку расширения %1 из каталога <%2>", ИмяРасширения, Каталог);

	ПараметрыЗапуска = Конфигуратор.ПолучитьПараметрыЗапуска();
	ПараметрыЗапуска.Добавить("/Visible");
	ПараметрыЗапуска.Добавить("/LoadConfigFromFiles """ + Каталог + """");
	ПараметрыЗапуска.Добавить("-Extension """ + ИмяРасширения + """");

	Если ОбновитьКонфигурациюИБ Тогда
		ПараметрыЗапуска.Добавить("/UpdateDBCfg");
	КонецЕсли;

	Конфигуратор.ВыполнитьКоманду(ПараметрыЗапуска);	

	Попытка
		ПоказатьСписокВсехРасширенийКонфигурации();
	Исключение
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Ошибка выгрузки расширения в файл %1",
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;

	Лог.Информация("Сборка/загрузка расширения %1 завершена", ИмяРасширения);

КонецПроцедуры

// Собирает из исходников расширение с указанным именем
//
// Параметры:
// 	ИмяРасширения - Строка - Имя расширения
//	Каталог - Строка - путь каталогу с иходниками расширения
//
Процедура РазобратьРасширениеНаИсходники(Знач ИмяРасширения, Знач Каталог) Экспорт

	Лог.Информация("Выполняю разборку расширения %1 на исходники в каталог <%2>", ИмяРасширения, Каталог);

	ПараметрыЗапуска = Конфигуратор.ПолучитьПараметрыЗапуска();
	ПараметрыЗапуска.Добавить("/Visible");
	ПараметрыЗапуска.Добавить("/DumpConfigToFiles """ + Каталог + """");
	ПараметрыЗапуска.Добавить("-Extension """ + ИмяРасширения + """");

	Попытка
		Конфигуратор.ВыполнитьКоманду(ПараметрыЗапуска);
	Исключение
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Ошибка выгрузки расширения в исходный код %1",
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;

	Лог.Информация("Разборка расширения завершена");

КонецПроцедуры

// Обновляет расширение в ИБ 1С
//
// Параметры:
//   ИмяРасширения - Строка - имя расширения
//
Процедура ОбновитьРасширение(Знач ИмяРасширения) Экспорт

	Лог.Информация("Запускаю обновление расширения %1", ИмяРасширения);

	Попытка
		Конфигуратор.ОбновитьКонфигурациюБазыДанных(Ложь, Ложь, Ложь, ИмяРасширения);
		Текст = Конфигуратор.ВыводКоманды();
		Если Не ПустаяСтрока(Текст) Тогда
			Лог.Информация(Текст);
		КонецЕсли;
	Исключение
		Лог.Ошибка(Конфигуратор.ВыводКоманды());
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Ошибка обновления расширения ""%1"": %2",
			ИмяРасширения,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;

	Лог.Информация("Обновление расширения завершено.");

КонецПроцедуры

// Выполняем запуск тестов для Vannessa Automation
//
// Параметры:
//	ПутьКФичам - Строка - Путь к фичам, может быть пустым
//	РабочийКаталогПроекта - Строка - Путь к каталогу с проектом, по умолчанию каталог ./
//	ПутьКНастройкам - Строка - Путь к файлу настроек запуска тестов
//	ПутьКИнструментам - Строка - пут Булево, Неопределено к инструментам, по умолчанию ВанессаАДД.ПутьИнструментаБДД()
//	ТолстыйКлиент - Булево, Неопределено - признак запуска толстого клиента
//	ОжидатьЗавершения - Булево - признак запуска ожидания, пока 1С завершится,
//				для разработки освобождения командной строки надо ставить Ложь;
//	ДопПараметры - Строка - дополнительные параметры для передачи в параметры запуска, например /DebugURLtcp://localhost
//	ТегиОтбор - Строка - Теги игнорирования фича-файлов
//	ТегиФильтр - Строка - Теги отбор фича-файлов
//	ДопКлючи - Строка - дополнительные параметры для передачи в параметры запуска /С, например NoLoadTestClientsTable
//
Процедура ВыполнитьТестированиеVanessaAutomation(Знач ПутьКФичам = Неопределено,
	Знач РабочийКаталогПроекта = Неопределено,
	Знач ПутьКНастройкам = "", Знач ПутьКИнструментам = "", Знач ТолстыйКлиент = Ложь,
	Знач ОжидатьЗавершения = Истина, Знач ДопПараметры = "",
	Знач ТегиОтбор = "", Знач ТегиФильтр = "",
	Знач ДопКлючи = "") Экспорт

	Лог.Информация("Начало тестирования с помощью фреймворка Vanessa Automation");

	Если РабочийКаталогПроекта = Неопределено Тогда
		РабочийКаталогПроекта = ".";
	КонецЕсли;
	РабочийКаталогПроекта = ОбщегоНазначения.ПолныйПуть(РабочийКаталогПроекта);
	УстановитьПеременнуюСреды("workspaceRoot", РабочийКаталогПроекта);
	Лог.Отладка("Рабочий каталог проекта <%1>", РабочийКаталогПроекта);
	УстановитьТекущийКаталог(РабочийКаталогПроекта);

	Если ПутьКФичам <> Неопределено Тогда
		ПутьКФичам = ОбщегоНазначения.ПолныйПуть(ПутьКФичам);
		Лог.Отладка("Абсолютный путь ПутьКФичам <%1>", ПутьКФичам);
		УстановитьПеременнуюСреды("VANESSA_FEATUREPATH", ПутьКФичам);
	КонецЕсли;
	ПутьКФичам = ФайловыеОперации.АбсолютныйПуть(ПутьКФичам);
	Если НЕ ФайловыеОперации.КаталогСуществует(ПутьКФичам) И НЕ ФайловыеОперации.ФайлСуществует(ПутьКФичам) Тогда
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Фичи файлы не найдены <%1>", ПутьКФичам);
	КонецЕсли;

	ПутьКИнструментам = ОбщегоНазначения.ПолныйПуть(ПутьКИнструментам);
	Лог.Отладка("Путь к Vanessa Automation %1", ПутьКИнструментам);
	ФайлСуществует = Новый Файл(ПутьКИнструментам).Существует();
	Текст = СтрШаблон("Ожидаем, что файл <%1> существует, а его нет!", ПутьКИнструментам);
	Ожидаем.Что(ФайлСуществует, Текст).ЭтоИстина();

	НазваниеКлючаСтатуса = "ПутьКФайлуДляВыгрузкиСтатусаВыполненияСценариев";
	НазваниеКлючаЛогаВыполнения = "ИмяФайлаЛогВыполненияСценариев";

	ПутьКФайлуСтатусаВыполнения = ОбъединитьПути(РабочийКаталогПроекта, "buildstatus.log");
	ПутьЛогаВыполненияСценариев = ОбъединитьПути(РабочийКаталогПроекта, "vanessaonline.txt");
	Если ФайловыеОперации.ФайлСуществует(ПутьКНастройкам) Тогда
		НастройкиЗапуска = ОбщегоНазначения.ПрочитатьФайлJSON(ПутьКНастройкам);
		Если НастройкиЗапуска.Получить(НазваниеКлючаСтатуса) <> Неопределено
			И НЕ ПустаяСтрока(НастройкиЗапуска.Получить(НазваниеКлючаСтатуса)) Тогда
			ПутьКФайлуСтатусаВыполнения = НастройкиЗапуска.Получить(НазваниеКлючаСтатуса);
		КонецЕсли;
		Если НастройкиЗапуска.Получить(НазваниеКлючаЛогаВыполнения) <> Неопределено 
			И НЕ ПустаяСтрока(НастройкиЗапуска.Получить(НазваниеКлючаЛогаВыполнения)) Тогда
			ПутьЛогаВыполненияСценариев = НастройкиЗапуска.Получить(НазваниеКлючаЛогаВыполнения);
		КонецЕсли;
	КонецЕсли;

	ПутьКФайлуСтатусаВыполнения = ФайловыеОперации.АбсолютныйПуть(ПутьКФайлуСтатусаВыполнения);
	ПутьЛогаВыполненияСценариев = ФайловыеОперации.АбсолютныйПуть(ПутьЛогаВыполненияСценариев);

	Лог.Информация("Путь к файлу статуса выполнения: <%1>", ПутьКФайлуСтатусаВыполнения);
	Лог.Информация("Путь к логу выполнения сценариев: <%1>", ПутьЛогаВыполненияСценариев);

	Если ПустаяСтрока(ДопКлючи) И НЕ Лев(ДопКлючи, 1) = ";" Тогда
		ДопКлючи = ";" + ДопКлючи;
	КонецЕсли;

	КлючЗапуска = """StartFeaturePlayer;ClearStepsCache;QuietInstallVanessaExt;"
					+ "VAParams=" + ПутьКНастройкам + ";"
					+ "WorkspaceRoot=" + РабочийКаталогПроекта
					+ ДопКлючи + """";

	Лог.Отладка(КлючЗапуска);

	ДополнительныеКлючи = " /TESTMANAGER " + ДопПараметры;

	ДопСообщения = НовыеДопСообщенияДляЗапускаПредприятия();
	ДопСообщения.Ключ = "ЗапуститьТестироватьПоведение";
	ДопСообщения.ПоказыватьДополнительноЛогПредприятия = Ложь;
	ДопСообщения.СообщениеВСлучаеУспеха = "Все фичи/сценарии выполнены!";
	ДопСообщения.СообщениеВСлучаеПадения = "Часть фич/сценариев упала!";
	ДопСообщения.СообщениеВСлучаеПропуска =
		"Ошибок при проверке поведения не найдено, но часть сценариев еще не реализована!";

	ЗапуститьВРежимеПредприятияСПроверкойВыполнения(
		ДопСообщения,
		КлючЗапуска, ПутьКИнструментам,
		ТолстыйКлиент, ДополнительныеКлючи, ОжидатьЗавершения,
		ПутьЛогаВыполненияСценариев, ПутьКФайлуСтатусаВыполнения);

	Лог.Информация("Тестирование поведения завершено");
	
КонецПроцедуры

// Создать структуру для дополнительных сообщений запуска 1С в режиме Предприятия
//
//  Возвращаемое значение:
//   Структура - ключи Ключ,СообщениеВСлучаеУспеха,СообщениеВСлучаеПадения,СообщениеВСлучаеПропуска с пустыми строками
//
Функция НовыеДопСообщенияДляЗапускаПредприятия() Экспорт
	Результат = Новый Структура("Ключ,СообщениеВСлучаеУспеха,СообщениеВСлучаеПадения",
		"", "", "");
	Результат.Вставить("СообщениеВСлучаеПропуска", "");
	Результат.Вставить("ПоказыватьДополнительноЛогПредприятия", Истина);
	Возврат Результат;
КонецФункции

// Выполнить команду/действие в режиме 1С:Предприятия
//	проверкой статус-файла выполнения и  возможностью ожидания выполнения и чтением лог-файла
//
// Параметры:
//   ДопСообщения - Структура - из метода НовыеДопСообщенияДляЗапускаПредприятия
//   ПараметрЗапуска - Строка - <описание параметра>
//   ОбработкаДляЗапуска - Строка - <описание параметра>
//   ТолстыйКлиент - Булево - признак запуска толстого клиента
//   ДополнительныеКлючиЗапуска - Строка - <описание параметра>
//   ОжидатьЗавершения - Булево - признак запуска толстого клиента
//   ПутьФайлаИнформации - Строка - путь файла информации 1С по ключу "/out".
//   ПутьКФайлуСтатусаВыполнения - Строка - путь файла статуса (внутри файла должно быть 1 или 0)
//
Процедура ЗапуститьВРежимеПредприятияСПроверкойВыполнения(
				Знач ДопСообщения,
				Знач ПараметрЗапуска,
				Знач ОбработкаДляЗапуска,
				Знач ТолстыйКлиент,
				Знач ДополнительныеКлючиЗапуска,
				Знач ОжидатьЗавершения,
				Знач ПутьЛогаВыполненияСценариев = Неопределено,
				Знач ПутьКФайлуСтатусаВыполнения = Неопределено) Экспорт

	Если Не ОжидатьЗавершения И ЗначениеЗаполнено(ПутьЛогаВыполненияСценариев) Тогда
		ВызватьИсключение "Нельзя получать лог выполнения без включенного признака ожидания выполнения 1С";
	КонецЕсли;

	Если ЗначениеЗаполнено(ПутьКФайлуСтатусаВыполнения) Тогда
		ФайловыеОперации.УдалитьФайлЕслиОнСуществует(ПутьКФайлуСтатусаВыполнения);
	КонецЕсли;
	Если ЗначениеЗаполнено(ПутьЛогаВыполненияСценариев) Тогда
		ФайловыеОперации.УдалитьФайлЕслиОнСуществует(ПутьЛогаВыполненияСценариев);
	КонецЕсли;

	ЛогОт1С = "";

	Попытка
		Если ОжидатьЗавершения Тогда
			ЛогОт1С = ЗапуститьВРежимеПредприятияСЛогФайлом(
				ПараметрЗапуска, ОбработкаДляЗапуска,
				ПутьЛогаВыполненияСценариев,
				ТолстыйКлиент, ДополнительныеКлючиЗапуска, ДопСообщения);
		Иначе
			ЗапуститьВРежимеПредприятия(
				ПараметрЗапуска, ОбработкаДляЗапуска,
				ТолстыйКлиент, ДополнительныеКлючиЗапуска, Ложь, ДопСообщения);
		КонецЕсли;

	Исключение
		Текст = ИнформацияОбОшибке().Описание;
		Лог.Ошибка("Причина ошибки:
			|%1
			|Вывод от 1С:Предприятие:
			|%2", Текст, ЛогОт1С);
		ВызватьИсключение;
	КонецПопытки;

	Если ЗначениеЗаполнено(ПутьКФайлуСтатусаВыполнения) Тогда
		СтатусВозврата = СокрЛП(ФайловыеОперации.ПрочитатьТекстФайла(ПутьКФайлуСтатусаВыполнения));
		Лог.Отладка("Код возврата %1", СтатусВозврата);
		Если СтатусВозврата = "0" Тогда
			Лог.Информация(ДопСообщения.СообщениеВСлучаеУспеха);
		ИначеЕсли СтатусВозврата = "1" Тогда
			ДанныеОшибки = Новый Структура;
			ДанныеОшибки.Вставить("Предупреждение", "
				|Vanessa Automation или 1С:Предприятие вернуло код возврата 1
				|");
			ВызватьИсключение Новый ИнформацияОбОшибке(ДопСообщения.СообщениеВСлучаеПадения, ДанныеОшибки);

		ИначеЕсли СтатусВозврата = "2" Тогда
			Лог.Предупреждение(ДопСообщения.СообщениеВСлучаеПропуска);
		Иначе
			// Если статус пуст, то вероятно были Warnings. Проверим текст, на словосочетания "БЫЛИ ОШИБКИ"
			Если СтрНайти(ЛогОт1С, "БЫЛИ ОШИБКИ") > 0 Тогда
				ТекстОшибки = СтрШаблон("Получен неожиданный/неверный результат работы - ""%1""
					|Возможно, работа 1С:Предприятие завершилась некорректно.
					|Например, указана неверная версия платформы, или возникла ошибка при запуске.
					|Проверьте журнал регистрации в ИБ. Вывод от 1С:Предприятие:
					|%2", СтатусВозврата, ЛогОт1С);
				ДанныеОшибки = Новый Структура;
				ДанныеОшибки.Вставить("Предупреждение", "");
				ВызватьИсключение Новый ИнформацияОбОшибке(ТекстОшибки, ДанныеОшибки);
			Иначе
				ТекстОшибки = СтрШаблон("Получен неожиданный результат работы - ""%1""
					|Вывод от 1С:Предприятие:
					|%2", СтатусВозврата, ЛогОт1С);
				Лог.Предупреждение(ЛогОт1С);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ПолучитьИнформациюОМетаданных() Экспорт

	Лог.Информация("Сбор информации о метаданных");
	ИмяФайла = ВременныеФайлы.НовоеИмяФайла();
	ОбработкаСборщик = ОбъединитьПути(ОбщегоНазначения.КаталогПроекта(), "tools", "СборИнформацииОМетаданных.epf");

    Если Не ФайловыеОперации.ФайлСуществует(ОбработкаСборщик) Тогда
        ВызватьИсключение СтрШаблон("Не обнаружена обработка сбора данных в каталоге '%1'", 
			ОбработкаСборщик);
    КонецЕсли;

    Попытка
		Конфигуратор.ЗапуститьВРежимеПредприятия("""" + ИмяФайла + """", Истина,
			"/Execute""" + ОбработкаСборщик + """");
	Исключение
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Не удалось получить информацию о метаданных при сборке: %1",
			ОписаниеОшибки())
	КонецПопытки;

	ИнформацияОМетаданных = ОбщегоНазначения.ПрочитьIniФайл(ИмяФайла);
	УдалитьФайлы(ИмяФайла);
	Лог.Информация("Сбор информации о конфигурации завершен");

КонецПроцедуры

// ToDo для тестов
// oscript actions.os -v infobase distrib --connection "/FC:\Projects\_Конфигурации\Телеграмм" --prefix "telegram" --path "C:\temp\123" --versions_path "C:\work\telegram" --versions_updates "1.0.0.10, 1.0.0.12" --minimal_platform_version "8.3.21.1644" --path_to_install "SoftOnIT" --destination "IT" --destination_demo "DemoIT"

// Выполнить сборку дистрибутива с обновлениями
//
// Параметры:
//   Параметры - Структура - параметры сборки дистрибутива
//		* КаталогСборки - Строка - каталог сборки дистрибутива
//
Процедура СоздатьДистрибутив(Знач Параметры) Экспорт
	
	Лог.Информация("Начало создания дистрибутива");

	// Удаляем все файлы в папке сборки, если она есть
	Если ФайловыеОперации.КаталогСуществует(Параметры.КаталогСборки) Тогда
		Попытка
			УдалитьФайлы(Параметры.КаталогСборки);
		Исключение
			ОбщегоНазначения.ЗавершениеРаботыОшибка("Не удалось удалить каталог со старой версией для новой сборки: %1",
				ОписаниеОшибки());
			Возврат;
		КонецПопытки;
	КонецЕсли;
	
	Ожидаем.Что(ИнформацияОМетаданных <> Неопределено, "Информация о метаданных не получена").ЕстьИстина();

	Префикс					= Параметры.Префикс;
	КаталогСборки			= Параметры.КаталогСборки;
	КаталогВерсийОбновлений = Параметры.КаталогВерсий;
	КаталогОбщихФайлов		= Параметры.КаталогОбщихФайлов;
	ВерсияКонфигурации 		= ИнформацияОМетаданных.Версия;
	ИмяКонфигурации 		= ИнформацияОМетаданных.Имя;
	СинонимКонфигурации 	= ИнформацияОМетаданных.Синоним;
	Поставщик				= Параметры.Поставщик; // Если поставщик задан в командной строке
	Если НЕ ЗначениеЗаполнено(Поставщик) Тогда
		Поставщик			= ИнформацияОМетаданных.Поставщик;
	КонецЕсли;
	ВерсииОбновления 		= Параметры.ВерсииОбновления;
	МинимальнаяВерсияПлатформы = Параметры.МинимальнаяВерсияПлатформы;
	МассивВерсий			= СтрРазделить(ВерсииОбновления, ",");

	Лог.Информация("Версия конфигурации %1", ВерсияКонфигурации);
	Лог.Информация("Имя конфигурации %1 (%2)", ИмяКонфигурации, СинонимКонфигурации);

	Лог.Информация("Создание каталогов и заполнение вспомогательных данных");
	КаталогВерсии 						= ОбъединитьПути(Параметры.КаталогСборки, ИнформацияОМетаданных.Версия);
	КаталогВерсииПолный 				= ОбъединитьПути(КаталогВерсии, "Полный");
	КаталогВерсииОбновление 			= ОбъединитьПути(КаталогВерсии, "Обновление");
	КаталогВерсииДополнительныеФайлы 	= ОбъединитьПути(КаталогВерсии, "Дополнительные файлы");	

	// Создаем каталоги
	ФайловыеОперации.ОбеспечитьКаталог(КаталогВерсии);
	ФайловыеОперации.ОбеспечитьКаталог(КаталогВерсииПолный);	
	ФайловыеОперации.ОбеспечитьКаталог(КаталогВерсииДополнительныеФайлы);

	// Нужно делать и обновление
	мСтрокаСВерсиями = "";
	Если МассивВерсий.Количество() > 0 Тогда		
		// Создаем файл UpdInfo.txt
		ФайловыеОперации.ОбеспечитьКаталог(КаталогВерсииОбновление);
		Лог.Информация("Создание файла UpdInfo.txt");
		ТД = Новый ТекстовыйДокумент;
		ТекстВерсий = "Version=" + ВерсияКонфигурации + Символы.ПС + "FromVersions=;";
		Для Каждого мВерсия Из МассивВерсий Цикл
			ТекстВерсий = ТекстВерсий + мВерсия + ";";
			Если НЕ ПустаяСтрока(мСтрокаСВерсиями) Тогда
				мСтрокаСВерсиями = мСтрокаСВерсиями + ", ";
			КонецЕсли;
			мСтрокаСВерсиями = мСтрокаСВерсиями + мВерсия;
		КонецЦикла;
		ТекстВерсий = ТекстВерсий + Символы.ПС + "UpdateDate=" + Формат(ТекущаяДата(), "ДФ=dd.MM.yyyy");
		ТД.УстановитьТекст(ТекстВерсий);
		ТД.Записать(ОбъединитьПути(КаталогВерсииДополнительныеФайлы, "UpdInfo.txt"));
	КонецЕсли;

	// Создаем ReadMe.txt
	Лог.Информация("Создание файла ReadMe.txt");
	ТД = Новый ТекстовыйДокумент;
	Текст = "Конфигурация """ + СинонимКонфигурации + """" + Символы.ПС + 
		"Версия " + ВерсияКонфигурации + Символы.ПС +
		"=================================================================" + Символы.ПС + Символы.ПС +		
		"Внимание!"+ Символы.ПС +
		"Текущая версия конфигурации """ + СинонимКонфигурации + """ предназначена"+ Символы.ПС +
		"для использования с версией системы 1С:Предприятие 8.3 не ниже " + МинимальнаяВерсияПлатформы;
	ТД.УстановитьТекст(Текст);
	ТД.Записать(ОбъединитьПути(КаталогВерсииДополнительныеФайлы, "ReadMe.txt"));

	// Создаем 1cv8upd.htm
	Лог.Информация("Создание файла 1cv8upd.htm");
	ТекстНовостей = "";
	// мФайлНовостей = Параметры["/News"];
	// Если ЗначениеЗаполнено(мФайлНовостей) Тогда		
	// 	Файл = Новый Файл(мФайлНовостей);
	// 	Если Файл.Существует() Тогда			
	// 		ТД.Прочитать(мФайлНовостей);
	// 		мТекстНовостей = ТД.ПолучитьТекст();
	// 	КонецЕсли;
	// КонецЕсли;
		
	Текст = "<HTML><HEAD>
			|<META http-equiv=Content-Type content=""text/html; charset=utf-8""><LINK href=""__STYLE__"" type=text/css rel=stylesheet><BASE href=v8config://1F802B99-D580-4E0F-93A3-F3F36B54052D/mdobject/id85895E9C-8000-4F0E-8DFD-B67B5FE6CC6C/8EB4FAD1-1FA6-403E-970F-2C12DBB43E23>
			|<META content=""MSHTML 6.00.2900.5969"" name=GENERATOR></HEAD>
			|<style type=""text/css"">
			|.updnew li:before{color:#090;content:""[+] "";font-family:""Courier New"",Courier,monospace;font-weight:bold;font-style:normal;list-style:none;}
			|.updedt li:before{color:#005EF9;content:""[*] "";font-family:""Courier New"",Courier,monospace;font-weight:bold;font-style:normal;list-style:none;}
			|.upderr li:before{color:#FF0000;content:""[-] "";font-family:""Courier New"",Courier,monospace;font-weight:bold;font-style:normal;list-style:none;}
			|.updnws li:before{color:#810A0A;content:""[#] "";font-family:""Courier New"",Courier,monospace;font-weight:bold;font-style:normal;list-style:none;}
			|</style>
			|<BODY>
			|<TABLE style=""BORDER-RIGHT: black 1px solid; BORDER-TOP: black 1px solid; BORDER-LEFT: black 1px solid; BORDER-BOTTOM: black 1px solid"" cellSpacing=0 cellPadding=8 width=""100%"" bgColor=#ffffd6 border=0>
			|<TBODY>
			|<TR>
			|<TD>
			|<H2><FONT face=Tahoma color=#c10000>Конфигурация<BR>" + СинонимКонфигурации + "</FONT></FONT></H2></TD></TR></TBODY></TABLE>
			|<H3 style=""BORDER-BOTTOM: #c10000 2px solid""><FONT face=Arial color=#c10000 size=5><EM>Версия&nbsp;" + ВерсияКонфигурации + "</EM></FONT></H3>
			|<H4><FONT face=Verdana color=#823602>Порядок обновления всех конфигураций</FONT></H4>
			|<OL>
			|<LI>Запустите систему 1С:Предприятие в режиме ""Конфигуратор"".
			|<LI><B>Обязательно сделайте архивную копию вашей информационной базы!</B> Для этого надо в меню Администрирование выбрать пункт ""Выгрузить информационную базу"" и ввести имя файла выгрузки. Этот файл надо сохранить в надёжном месте.
			|<LI>В режиме ""Конфигуратор"" откройте конфигурацию, для этого в меню ""Конфигурация"" выберите пункт ""Открыть конфигурацию"".
			|<LI>Вызовите режим ""Обновление конфигураций"", для этого в меню ""Конфигурация"", подменю ""Поддержка"", выберите пункт ""Обновить конфигурацию"".
			|<LI>В диалоге выбора обновления в качестве источника обновления укажите ""Доступные обновления"", после чего выберите нужное обновление в соответствующем списке.
			|<LI>Если в списке обновлений необходимое обновление отсутствует, то в диалоге выбора обновления в качестве источника обновления укажите ""Файл обновления"", после чего выберите нужный файл обновления (по умолчанию 1cv8.cfu).
			|<LI>В окне ""Обновление конфигураций"" нажмите кнопку ""OK"" для продолжения обновления конфигурации.
			|<LI>На вопрос об обновлении конфигурации базы данных ответьте ""ДА"".
			|<LI>После завершения <B>обязательно</B> запустите программу в режиме ""Предприятие"" - для совершения конвертации. Некоторые пользователи не выходя из конфигуратора последовательно обновляют версии - это недопустимо и повлечет за собой невозможность дальнейшей работы. После каждого обновления надо хотя бы 1 раз запускать 1С в режиме 1С:Предприятие.</LI></OL>
			|<H4><FONT face=Verdana color=#823602>Порядок обновления конфигурации версий " + мСтрокаСВерсиями + " на версию " + ВерсияКонфигурации + "</FONT></H4>
			|<P>Для обновления версии конфигурации следует использовать режим ""Обновление конфигураций"". Файл обновлений 1Cv8.cfu находится в каталоге шаблонов (по умолчанию - подкаталог tmplts\ каталога установки 1С:Предприятия 8), в подкаталоге SoftOnIT\it3\" + СтрЗаменить(ВерсияКонфигурации, ".", "_") + "\</P>
			|</SPAN><hr><font color=""#ff0000"">Внимание!<br>Текущая версия конфигурации """ + СинонимКонфигурации + """ предназначена для использования с версией системы 1С:Предприятие 8.3 не ниже " + МинимальнаяВерсияПлатформы + ".</font><hr>" + 
			ТекстНовостей + "
			|</BODY></HTML>";
	ТД.УстановитьТекст(Текст);
	ТД.Записать(ОбъединитьПути(КаталогВерсииДополнительныеФайлы, "1cv8upd.htm"));
		
	// ШАГ 5. Создаем файл поставки
	ПараметрыЗапуска3 = Конфигуратор.ПолучитьПараметрыЗапуска();
	ПараметрыЗапуска3.Добавить("/CreateDistributionFiles");
	// Создаем дистрибутив
	ПараметрыЗапуска3.Добавить("-cffile """ + ОбъединитьПути(КаталогВерсии, "1Cv8.cf") + """");
	Если МассивВерсий.Количество() > 0 Тогда
		// Если надо, создаем обновления
		ПараметрыЗапуска3.Добавить("-cfufile """ + ОбъединитьПути(КаталогВерсии, "1Cv8.cfu") + """");
		Для Каждого мВерсия Из МассивВерсий Цикл
			ПараметрыЗапуска3.Добавить("-f """ + ОбъединитьПути(КаталогВерсийОбновлений, СокрЛП(мВерсия), "1Cv8.cf") + """");
		КонецЦикла;
	КонецЕсли;

	Лог.Информация("Создание файла поставки");
	СтрПараметры = "";
	Для Каждого Стр Из ПараметрыЗапуска3 Цикл
		СтрПараметры = СтрПараметры + Стр + " ";
	КонецЦикла;
	Лог.Информация("Параметры файла поставки: " + СтрПараметры);
	
	Попытка		
		Конфигуратор.ВыполнитьКоманду(ПараметрыЗапуска3);
		Лог.Информация("Создание файла поставки завершено");
	Исключение												   
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Произошла ошибка при создании файла поставки %1",
			Конфигуратор.ВыводКоманды());
	КонецПопытки;

	// Создание файла манифеста 1cv8.mft
	// ToDo
	Создать_1cv8_mft(КаталогВерсии, Поставщик, Параметры.КаталогШаблоновПоставщика,
		Параметры.Назначение, Параметры.НазначениеДемо);

	// Создание файла манифеста install.edf
	// ToDo
	ИмяФайлаМанифеста = ОбъединитьПути(КаталогСборки, "install.edf");
	Лог.Информация("Создание файла манифеста %1", ИмяФайлаМанифеста);
	СоздатьФайлМанифеста(ИмяФайлаМанифеста, Префикс, Поставщик, КаталогСборки, КаталогОбщихФайлов);
		
	// Создание дистрибутива
	ПараметрыЗапуска4 = Конфигуратор.ПолучитьПараметрыЗапуска();
	ПараметрыЗапуска4.Добавить("/CreateDistributive """ + КаталогВерсииПолный + """");
	ПараметрыЗапуска4.Добавить("-File """ + ИмяФайлаМанифеста + """");
	ПараметрыЗапуска4.Добавить("-MakeSetup");
	
	Лог.Информация("Создание дистрибутива");
	Попытка		
		Конфигуратор.ВыполнитьКоманду(ПараметрыЗапуска4);
		Лог.Информация("Создание дистрибутива завершено");
		
		// Делаем архив
		ИмяАрхива = ОбъединитьПути(КаталогВерсииПолный, Префикс + СтрЗаменить(ВерсияКонфигурации, ".", "_") + "_full.zip");
		Лог.Информация("Имя файла с архивом <%1>", ИмяАрхива);
		ЗаписьZIP =  Новый ЗаписьZipФайла(ИмяАрхива);
		ЗаписьZIP.Добавить(ОбъединитьПути(КаталогВерсииПолный, "*.*"),
			РежимСохраненияПутейZIP.СохранятьОтносительныеПути,
			РежимОбработкиПодкаталоговZIP.ОбрабатыватьРекурсивно);		
		ЗаписьZIP.Записать();
		Лог.Информация("Создание архива дистрибутива завершено");
		
	Исключение												   
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Произошла ошибка при создании дистрибутива %1",
			Конфигуратор.ВыводКоманды());
	КонецПопытки;
	
	// ШАГ 7. Создание обновления
	Если МассивВерсий.Количество() > 0 Тогда
		ПараметрыЗапуска5 = Конфигуратор.ПолучитьПараметрыЗапуска();
		ПараметрыЗапуска5.Добавить("/CreateDistributive """ + КаталогВерсииОбновление + """");
		ПараметрыЗапуска5.Добавить("-File """ + ОбъединитьПути(КаталогСборки, "install.edf") + """");
		ПараметрыЗапуска5.Добавить("-Option Обновление");
		ПараметрыЗапуска5.Добавить("-MakeSetup");

		Лог.Информация("Создание обновления дистрибутива");
		Попытка
			Конфигуратор.ВыполнитьКоманду(ПараметрыЗапуска5);
			Лог.Информация("Создание обновления дистрибутива завершено");
			
			// Делаем архив
			ИмяАрхива = ОбъединитьПути(КаталогВерсииОбновление, Префикс + СтрЗаменить(ВерсияКонфигурации, ".", "_") + "_upd.zip");
			ЗаписьZIP =  Новый ЗаписьZipФайла(ИмяАрхива);     
			ЗаписьZIP.Добавить(ОбъединитьПути(КаталогВерсииОбновление, "*.*"),
				РежимСохраненияПутейZIP.СохранятьОтносительныеПути,
				РежимОбработкиПодкаталоговZIP.ОбрабатыватьРекурсивно);
			ЗаписьZIP.Записать();
			Лог.Информация("Создание архива обновления дистрибутива завершено");
			
		Исключение												   
			ОбщегоНазначения.ЗавершениеРаботыОшибка("Произошла ошибка при создании обновления дистрибутива %1",
				Конфигуратор.ВыводКоманды());
		КонецПопытки;
			
	КонецЕсли;	

	Лог.Информация("Успешное завершение сборки.");

КонецПроцедуры

Процедура Создать_1cv8_mft(Знач КаталогСборки, Знач Поставщик, Знач КаталогШаблоновПоставщика, 
	Знач Назначение, Знач НазначениеДемо)
	
	Лог.Информация("Начало создания файла 1cv8.mft в %1", КаталогСборки);	
	
	// Генерируем новый файл 1cv8.mft
	Файл = Новый ТекстовыйДокумент;
	Файл.ДобавитьСтроку("Vendor=" + Поставщик);
	Файл.ДобавитьСтроку("Name=" + ИнформацияОМетаданных.Имя);
	Файл.ДобавитьСтроку("Version=" + ИнформацияОМетаданных.Версия);
	Файл.ДобавитьСтроку("AppVersion=8.3");
	Файл.ДобавитьСтроку("[Config1]");
	Файл.ДобавитьСтроку("Catalog=" + ИнформацияОМетаданных.Синоним);
	Файл.ДобавитьСтроку("Destination=" + КаталогШаблоновПоставщика + "\" + Назначение);
	Файл.ДобавитьСтроку("Source=1Cv8.cf");
	Файл.ДобавитьСтроку("[Config2]");
	Файл.ДобавитьСтроку("Catalog=" + ИнформацияОМетаданных.Синоним + " (демо)");
	Файл.ДобавитьСтроку("Destination=" + КаталогШаблоновПоставщика + "\" + НазначениеДемо);
	Файл.ДобавитьСтроку("Source=1Cv8.dt");
	Файл.Записать(ОбъединитьПути(КаталогСборки, "1cv8.mft"));

	Лог.Информация("Файл 1cv8.mft создан");	

КонецПроцедуры

Процедура СоздатьФайлМанифеста(Знач ИмяФайла, Знач Префикс, Знач Поставщик, Знач КаталогСборки, Знач КаталогОбщихФайлов)

	Лог.Информация("Начало создания файла манифеста %1", ИмяФайла);
	
	// BSLLS:LatinAndCyrillicSymbolInWord-off
	// BSLLS:DuplicateStringLiteral-off

	// Генерируем новый файл install.edf
	Версия = ИнформацияОМетаданных.Версия; // "3.1.7.7";

	ВерсияПодчеркивания = СтрЗаменить(Версия, ".", "_");
	КаталогСборкиВерсия = ОбъединитьПути(КаталогСборки, Версия);

	ИмяКаталогаДополнительныхФайлов = "Дополнительные файлы";
	Файл1cv8upd = ОбъединитьПути(КаталогСборкиВерсия, ИмяКаталогаДополнительныхФайлов, "1cv8upd.htm");
	ФайлReadMe 	= ОбъединитьПути(КаталогСборкиВерсия, ИмяКаталогаДополнительныхФайлов, "ReadMe.txt");
	ФайлUpdInfo = ОбъединитьПути(КаталогСборкиВерсия, ИмяКаталогаДополнительныхФайлов, "UpdInfo.txt");
	Файл1cv8mft = ОбъединитьПути(КаталогСборкиВерсия, "1Cv8.mft");
	Файл1cv8cfu = ОбъединитьПути(КаталогСборкиВерсия, "1Cv8.cfu");

	Имя 		= СтрокаМанифеста(ИнформацияОМетаданных.Имя);
	Поставщик 	= СтрокаМанифеста(Поставщик);
	Синоним 	= СтрокаМанифеста(ИнформацияОМетаданных.Синоним);
	Префикс 	= СтрокаМанифеста(Префикс);	

	// Генерируем новый файл install.edf
	Файл = Новый ТекстовыйДокумент;
	Файл.ДобавитьСтроку("{1,");
	Файл.ДобавитьСтроку("{");
	Файл.ДобавитьСтроку("{1,2,");
	Файл.ДобавитьСтроку("{""en"",""" + Поставщик + """},");
	Файл.ДобавитьСтроку("{""ru"",""" + Поставщик + """}");
	Файл.ДобавитьСтроку("},");
	Файл.ДобавитьСтроку("{1,2,");
	Файл.ДобавитьСтроку("{""en"",""" + Синоним + """},");
	Файл.ДобавитьСтроку("{""ru"",""" + Синоним + """}");
	Файл.ДобавитьСтроку("},");
	Файл.ДобавитьСтроку("{#base64:}");
	Файл.ДобавитьСтроку("},");
	Файл.ДобавитьСтроку("{1,");
	Файл.ДобавитьСтроку("{1,0,""" + Имя + """,""" + Поставщик + """,""" + Версия + """,""SoftOnIT\" + Префикс + "\" + ВерсияПодчеркивания + """,");
	Файл.ДобавитьСтроку("{");
	Файл.ДобавитьСтроку("{""/"",");
	Файл.ДобавитьСтроку("{7,");
	Файл.ДобавитьСтроку("{c61a3081-db7b-4d57-b836-150d73beff1f,""Файл конфигурации"",1,00000000-0000-0000-0000-000000000000,""" + Синоним + ""","""",00000000-0000-0000-0000-000000000000,"""",1,1,1,1},");
	Файл.ДобавитьСтроку("{ed7bf966-330d-46f0-9bf0-57a0db7ecd04,""Файл выгрузки информационной базы"",1,00000000-0000-0000-0000-000000000000,""" + Синоним + " (демо)"","""",00000000-0000-0000-0000-000000000000,"""",0,2,1,1},");
	Файл.ДобавитьСтроку("{a73646ed-6000-4541-b5d7-3b76985639f8,""1cv8upd.htm"",0,"""",00000000-0000-0000-0000-000000000000,""" + Файл1cv8upd + """,0,0,0,0},");
	Файл.ДобавитьСтроку("{49e79e4d-ce7d-4197-b966-a5082632a23e,""ReadMe.txt"",0,"""",00000000-0000-0000-0000-000000000000,""" + ФайлReadMe + """,0,0,0,0},");
	Файл.ДобавитьСтроку("{14503a27-1ad8-472e-a13b-2aedfca7e89f,""UpdInfo.txt"",0,"""",00000000-0000-0000-0000-000000000000,""" + ФайлUpdInfo + """,0,0,0,0},");
	Файл.ДобавитьСтроку("{9def14b2-01de-46a0-9550-d509dcf78d74,""1Cv8.cfu"",0,"""",00000000-0000-0000-0000-000000000000,""" + Файл1cv8cfu + """,0,0,0,0},");
	Файл.ДобавитьСтроку("{87728706-8f7f-464d-9ec2-bb85454faa27,""1cv8.mft"",0,"""",00000000-0000-0000-0000-000000000000,""" + Файл1cv8mft + """,0,0,0,0}");
	Файл.ДобавитьСтроку("},");
	Если ЗначениеЗаполнено(КаталогОбщихФайлов) Тогда
		КаталогОбщихФайлов = СтрЗаменить(КаталогОбщихФайлов, """", "");
		КаталогОбщихФайлов = ФайловыеОперации.ДополнитьРазделителемПути(КаталогОбщихФайлов);
		Файл.ДобавитьСтроку("{1,");
		Файл.ДобавитьСтроку("{366f16f3-e8bf-455a-a547-b990015c4a94,""Общие файлы"",00000000-0000-0000-0000-000000000000,""" + КаталогОбщихФайлов + """,""*.*"",1}");
		Файл.ДобавитьСтроку("},");
	Иначе
		Файл.ДобавитьСтроку("{0},");
	КонецЕсли;
	Файл.ДобавитьСтроку("{0}");
	Файл.ДобавитьСтроку("}");
	Файл.ДобавитьСтроку("}");
	Файл.ДобавитьСтроку("}");
	Файл.ДобавитьСтроку("},");
	Файл.ДобавитьСтроку("{0},");
	Файл.ДобавитьСтроку("{2,""Полный"",0,");
	Файл.ДобавитьСтроку("{0},00000000-0000-0000-0000-000000000000,"""",00000000-0000-0000-0000-000000000000,"""",""Обновление"",1,""ru"",a73646ed-6000-4541-b5d7-3b76985639f8,");
	Файл.ДобавитьСтроку("{5,9def14b2-01de-46a0-9550-d509dcf78d74,a73646ed-6000-4541-b5d7-3b76985639f8,49e79e4d-ce7d-4197-b966-a5082632a23e,14503a27-1ad8-472e-a13b-2aedfca7e89f,366f16f3-e8bf-455a-a547-b990015c4a94},00000000-0000-0000-0000-000000000000,"""",00000000-0000-0000-0000-000000000000,""""},");
	Файл.ДобавитьСтроку("{0}");
	Файл.ДобавитьСтроку("}");
	Файл.Записать(ИмяФайла);	

	Лог.Информация("Файл создан");

	// BSLLS:LatinAndCyrillicSymbolInWord-on
	// BSLLS:DuplicateStringLiteral-on

КонецПроцедуры

Функция СтрокаМанифеста(Знач Строка)

	Возврат СтрЗаменить(Строка, """", """""");

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполнить команду/действие в режиме 1С:Предприятия
//
// Параметры:
//   ПараметрЗапуска - Строка - <описание параметра>
//   ОбработкаДляЗапуска - Строка - <описание параметра>
//   ТолстыйКлиент - Булево - признак запуска толстого клиента
//   ДополнительныеКлючиЗапуска - Строка - <описание параметра>
//   ОжидатьЗавершения - Булево - по умолчанию Истина, Ложь - запускает и завершает свой процесс.
//   ДопСообщения - Структура - сообщение для вывода
//
Процедура ЗапуститьВРежимеПредприятия(Знач ПараметрЗапуска,
	Знач ОбработкаДляЗапуска, Знач ТолстыйКлиент,
	Знач ДополнительныеКлючиЗапуска,
	Знач ОжидатьЗавершения, Знач ДопСообщения)

	Лог.Информация("Выполняю команду/действие в режиме 1С:Предприятие");

	ТекущаяПроцедура = ДопСообщения.Ключ;

	Если ТолстыйКлиент = Ложь Тогда
		ТонкийКлиент1С = Конфигуратор.ПутьКТонкомуКлиенту1С(Конфигуратор.ПутьКПлатформе1С());
		Конфигуратор.ПутьКПлатформе1С(ТонкийКлиент1С);
	КонецЕсли;

	Конфигуратор.УстановитьПризнакОжиданияВыполненияПрограммы(ОжидатьЗавершения);

	Если Не ОжидатьЗавершения Тогда
		Конфигуратор.УстановитьИмяФайлаСообщенийПлатформы(ВременныеФайлы.НовоеИмяФайла());
	КонецЕсли;

	ДополнительныеКлючи = ДополнительныеКлючиЗапуска;
	Если Не ПустаяСтрока(ОбработкаДляЗапуска) Тогда
		ДополнительныеКлючи = "" + ДополнительныеКлючи + " /Execute" 
			+ ФайловыеОперации.ОбернутьПутьВКавычки(ОбработкаДляЗапуска);
	КонецЕсли;

	Лог.Отладка("ДополнительныеКлючи:" + ДополнительныеКлючи);
	Лог.Отладка("ПараметрЗапуска:" + ПараметрЗапуска);

	Попытка
		Конфигуратор.ЗапуститьВРежимеПредприятия(ПараметрЗапуска,
			?(ТипЗнч(ТолстыйКлиент) = Тип("Булево"), Не ТолстыйКлиент, ТолстыйКлиент),
			ДополнительныеКлючи
		);
		Текст = Конфигуратор.ВыводКоманды();
		Если Не ПустаяСтрока(Текст) Тогда
			Лог.Информация(Текст);
		КонецЕсли;

	Исключение
		Лог.Ошибка(Конфигуратор.ВыводКоманды());
		Лог.Ошибка(ОписаниеОшибки());
		ВызватьИсключение ТекущаяПроцедура;
	КонецПопытки;

	Лог.Информация("Выполнение команды/действия в режиме 1С:Предприятие завершено.");
КонецПроцедуры

// Выполнить команду/действие в режиме 1С:Предприятия с ожиданием выполнения и чтением лог-файла
//
// Параметры:
//   ПараметрЗапуска - Строка - <описание параметра>
//   ОбработкаДляЗапуска - Строка - <описание параметра>
//   ПутьЛогаВыполненияСценариев - Строка - путь файла информации 1С по ключу "/out".
//   ТолстыйКлиент - Булево, Неопределено - признак запуска толстого клиента
//   ДополнительныеКлючиЗапуска - Строка - <описание параметра>
//   ДопСообщения - Строка - доп. сообщения для вывода
//
Функция ЗапуститьВРежимеПредприятияСЛогФайлом(Знач ПараметрЗапуска,
										Знач ОбработкаДляЗапуска,
										Знач ПутьЛогаВыполненияСценариев,
										Знач ТолстыйКлиент,
										Знач ДополнительныеКлючиЗапуска,
										Знач ДопСообщения)

	Лог.Информация("Выполняю команду/действие в режиме 1С:Предприятие");

	Результат = "";

	ТекущаяПроцедура = "ЗапуститьВРежимеПредприятияСЛогФайлом";

	Конфигуратор.УстановитьПризнакОжиданияВыполненияПрограммы(Истина);

	ПутьДамп = ВременныеФайлы.НовоеИмяФайла();
	Конфигуратор.УстановитьИмяФайлаСообщенийПлатформы(ПутьДамп);

	ДополнительныеКлючи = ДополнительныеКлючиЗапуска;
	Если Не ПустаяСтрока(ОбработкаДляЗапуска) Тогда
		ДополнительныеКлючи = "" + ДополнительныеКлючи + " /Execute"
			+ ФайловыеОперации.ОбернутьПутьВКавычки(ОбработкаДляЗапуска);
	КонецЕсли;

	Лог.Отладка("ДополнительныеКлючи:" + ДополнительныеКлючи);
	Лог.Отладка("ПараметрЗапуска:" + ПараметрЗапуска);

	ПараметрыСвязиСБазой = Конфигуратор.ПолучитьПараметрыЗапуска();
	ПараметрыСвязиСБазой[0] = "ENTERPRISE";
	ПараметрыСвязиСБазой.Удалить(2);

	Если ЗначениеЗаполнено(ПараметрЗапуска) Тогда
		ПараметрыСвязиСБазой.Добавить("/C" + ПараметрЗапуска);
	КонецЕсли;

	Если ТолстыйКлиент = Истина Тогда
		ПараметрыСвязиСБазой.Добавить("/RunModeOrdinaryApplication ");
	КонецЕсли;

	ПараметрыСвязиСБазой.Добавить("/out""" + ПутьДамп + """");

	Если ДополнительныеКлючи <> Неопределено Тогда
		ПараметрыСвязиСБазой.Добавить(ДополнительныеКлючи);
	КонецЕсли;

	СтрокаЗапуска = "";
	Для Каждого Параметр Из ПараметрыСвязиСБазой Цикл
		СтрокаЗапуска = СтрокаЗапуска + " " + Параметр;
	КонецЦикла;

	Приложение = Конфигуратор.ПутьКТонкомуКлиенту1С();
	Если ТолстыйКлиент = Истина Тогда
		Приложение = Конфигуратор.ПутьКПлатформе1С();
	КонецЕсли;

	Если Найти(Приложение, " ") > 0 Тогда
		Приложение = ФайловыеОперации.ОбернутьПутьВКавычки(Приложение);
	КонецЕсли;
	Приложение = Приложение + " " + СтрокаЗапуска;
	Лог.Отладка(Приложение);

	Попытка
		ЗапуститьПроцесс1С(Приложение, ПутьЛогаВыполненияСценариев);
		Результат = ПоказатьЛогПредприятия(ПутьДамп, ДопСообщения.ПоказыватьДополнительноЛогПредприятия);

	Исключение
		ОписаниеОшибки = ОписаниеОшибки();

		Результат = ПоказатьЛогПредприятия(ПутьДамп, Истина);

		Лог.Ошибка(ОписаниеОшибки);
		ВызватьИсключение ТекущаяПроцедура;
	КонецПопытки;

	Лог.Информация("Выполнение команды/действия в режиме 1С:Предприятие завершено.");

	Возврат Результат;
КонецФункции

Процедура ЗапуститьПроцесс1С(Знач СтрокаЗапуска, Знач ПутьКФайлуЛога)

	ПериодОпросаВМиллисекундах = 1000;

	НадоЧитатьЛог = Истина;
	КолСтрокЛогаПрочитано = 0;

	Процесс = СоздатьПроцесс(СтрокаЗапуска);
	Процесс.Запустить();

	ТаймаутПоУмолчанию = 500;
	Приостановить(ТаймаутПоУмолчанию);

	Пока НЕ Процесс.Завершен Цикл
		Если ПериодОпросаВМиллисекундах <> 0 Тогда
			Приостановить(ПериодОпросаВМиллисекундах);
		КонецЕсли;

		Если НадоЧитатьЛог Тогда
			ВывестиНовыеСообщения(ПутьКФайлуЛога, КолСтрокЛогаПрочитано);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Процедура ВывестиНовыеСообщения(ИмяФайлаЛога, КолСтрокЛогаПрочитано)

	Попытка
		МассивСтрок = ПолучитьНовыеСтрокиЛога(ИмяФайлаЛога, КолСтрокЛогаПрочитано);
		Для Каждого Стр Из МассивСтрок Цикл
			Если СокрЛП(Стр) = "" Тогда
				Продолжить;
			КонецЕсли;
			Лог.Информация(СокрЛП(Стр));
		КонецЦикла;
	Исключение
		Лог.Ошибка(ОписаниеОшибки());
	КонецПопытки;

КонецПроцедуры

Функция ПолучитьНовыеСтрокиЛога(Знач ИмяФайла, КолСтрокЛогаПрочитано)

	Файл = Новый Файл(ИмяФайла);
	Если Не Файл.Существует() Тогда
		Возврат Новый Массив;
	КонецЕсли;

	Текст = Новый ЧтениеТекста;
	Текст.Открыть(ИмяФайла, "UTF-8", , , Ложь);

	ВесьТекст = Текст.Прочитать();

	Текст.Закрыть();

	Массив = Новый Массив();

	МассивСтрок = СтрРазделить(ВесьТекст, Символы.ПС, Истина);
	Если ЗначениеЗаполнено(МассивСтрок) Тогда
		КоличествоМинус1 = МассивСтрок.Количество() - 1;
		Если МассивСтрок[КоличествоМинус1] = "" Тогда
			МассивСтрок.Удалить(КоличествоМинус1);
		КонецЕсли;
	КонецЕсли;

	Для Ккк = (КолСтрокЛогаПрочитано + 1) По МассивСтрок.Количество() Цикл
		Массив.Добавить(МассивСтрок[Ккк - 1]);
	КонецЦикла;

	КолСтрокЛогаПрочитано = МассивСтрок.Количество();

	Возврат Массив;
КонецФункции

Функция ПоказатьЛогПредприятия(Знач ПутьДамп, Знач ПоказыватьДополнительноЛогПредприятия)
	Результат = "";
	Если ФайловыеОперации.ФайлСуществует(ПутьДамп) Тогда
		Результат = ФайловыеОперации.ПрочитатьТекстФайла(ПутьДамп, КодировкаТекста.ANSI);

		Если Не ПустаяСтрока(Результат) Тогда
			Сообщение = СтрШаблон("Дополнительный лог выполнения 1С:Предприятие
			|
			|%1", Результат);
			Если ПоказыватьДополнительноЛогПредприятия Тогда
				Лог.Информация(Сообщение);
			Иначе
				Лог.Отладка(Сообщение);
			КонецЕсли;
		КонецЕсли;
	Иначе
		Лог.Отладка("Не существует файл вывода от 1С - %1", ПутьДамп);
	КонецЕсли;

	Возврат Результат;

КонецФункции

#КонецОбласти

Лог = ПараметрыСистемы.Лог();