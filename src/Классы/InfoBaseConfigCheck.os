#Область ПрограммныйИнтерфейс

// Процедура - устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект описание команды
//
Процедура ОписаниеКоманды(Команда) Экспорт
			
	// Добавление дополнительных опций
	ПараметрыОпций = Работа1С.КонструкторПроверкиКонфигурации();
	Работа1С.ОписаниеКоманды(Команда, ПараметрыОпций);
	
	Команда.Опция("file f", "",
		"Запись результатов проверки в файл (не обязательный)")
		.ТСтрока();

	КлючиПроверки = Работа1С.КлючиПроверкиКонфигурации();

	Для Каждого КлючЗначение Из КлючиПроверки Цикл
		Команда.Опция(КлючЗначение.Ключ, "",
			КлючЗначение.Значение)
			.ТБулево();
	КонецЦикла;

КонецПроцедуры // ОписаниеКоманды()

// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	
	Параметры = ПолучитьСтруктуруПараметров(Команда);
	МенеджерКонфигуратора = Новый МенеджерКонфигуратора();
	МенеджерКонфигуратора.Инициализация(Параметры);
	МенеджерКонфигуратора.ВыполнитьПроверкуКонфигурации(Параметры.ПараметрыПроверки, Параметры.ИмяФайла);
	МенеджерКонфигуратора.Деструктор();
	
КонецПроцедуры // ВыполнитьКоманду()

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСтруктуруПараметров(Знач Команда)
	
	ЧтениеОпций = Новый ЧтениеОпцийКоманды(Команда);
	ВыводОтладочнойИнформации = ЧтениеОпций.ЗначениеОпции("verbose");
	ПараметрыСистемы.УстановитьРежимОтладки(ВыводОтладочнойИнформации);
	
	ПараметрыОпций = Работа1С.КонструкторПроверкиКонфигурации();	
	ПараметрыКоманды = Работа1С.ПрочитатьПараметры(Команда, ПараметрыОпций);	

	ПараметрыКоманды.Вставить("ИмяФайла", ЧтениеОпций.ЗначениеОпции("file"));

	// Получаем ключи проверки
	КлючиПроверки = Работа1С.КлючиПроверкиКонфигурации();
	ПараметрыПроверки = Новый Структура();
	Для Каждого КлючЗначение Из КлючиПроверки Цикл
		КлючАктивен = ЧтениеОпций.ЗначениеОпции(КлючЗначение.Ключ, Ложь);
		Если ЗначениеЗаполнено(КлючАктивен) Тогда
			ПараметрыПроверки.Вставить(КлючЗначение.Ключ, Истина);
		КонецЕсли;
	КонецЦикла;

	ПараметрыКоманды.Вставить("ПараметрыПроверки", ПараметрыПроверки);
	
	Возврат ПараметрыКоманды;
	
КонецФункции // ПолучитьСтруктуруПараметров()

#КонецОбласти // СлужебныеПроцедурыИФункции