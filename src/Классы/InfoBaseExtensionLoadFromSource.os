#Область ПрограммныйИнтерфейс

// Процедура - устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект описание команды
//
Процедура ОписаниеКоманды(Команда) Экспорт
			
	// Добавление дополнительных опций
	ПараметрыОпций = Работа1С.КонструкторОбщий();
	Работа1С.ОписаниеКоманды(Команда, ПараметрыОпций);

	Команда.Опция("path from p", "",
		"Путь к исходникам расширения (обязательный)")
		.ТСтрока();

	Команда.Опция("name extension n", "",
		"Имя расширения конфигурации 1С под которым оно будет зарегистрировано (обязательный)")
		.ТСтрока();

	Команда.Опция("update u", "",
		"Признак обновления расширения. Имеет смысл только на пустой базе или при первой загрузке (не обязательный)")
		.ТБулево();	

КонецПроцедуры // ОписаниеКоманды()

// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	
	ПараметрыКоманды = ПолучитьСтруктуруПараметров(Команда);
	МенеджерКонфигуратора = Новый МенеджерКонфигуратора();
	МенеджерКонфигуратора.Инициализация(ПараметрыКоманды);
	Попытка
		МенеджерКонфигуратора.СобратьИзИсходниковРасширение(ПараметрыКоманды.ПутьКФайлу, 
			ПараметрыКоманды.ИмяРасширения,
			ПараметрыКоманды.ОбновитьКонфигурациюИБ);		
	Исключение
		МенеджерКонфигуратора.Деструктор();
		ОбщегоНазначения.ЗавершениеРаботыОшибка("Ошибка загрузки расширения %1 из файла <%2>: %3",
			ПараметрыКоманды.ИмяРасширения,
			ПараметрыКоманды.ПутьКФайлу,			
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	МенеджерКонфигуратора.Деструктор();
	
КонецПроцедуры // ВыполнитьКоманду()

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСтруктуруПараметров(Знач Команда)
	
	ЧтениеОпций = Новый ЧтениеОпцийКоманды(Команда);
	ВыводОтладочнойИнформации = ЧтениеОпций.ЗначениеОпции("verbose");
	ПараметрыСистемы.УстановитьРежимОтладки(ВыводОтладочнойИнформации);
	
	ПараметрыОпций = Работа1С.КонструкторПроверкиКонфигурации();	
	ПараметрыКоманды = Работа1С.ПрочитатьПараметры(Команда, ПараметрыОпций);
	ПараметрыКоманды.Вставить("ПутьКФайлу", ЧтениеОпций.ЗначениеОпции("path", Истина));
	ПараметрыКоманды.Вставить("ИмяРасширения", ЧтениеОпций.ЗначениеОпции("name", Ложь));
	ПараметрыКоманды.Вставить("ОбновитьКонфигурациюИБ", ЧтениеОпций.ЗначениеОпции("update", Ложь));
	ПараметрыКоманды.ОбновитьКонфигурациюИБ = НЕ ЗначениеЗаполнено(ПараметрыКоманды.ОбновитьКонфигурациюИБ);

	Возврат ПараметрыКоманды;
	
КонецФункции // ПолучитьСтруктуруПараметров()

#КонецОбласти // СлужебныеПроцедурыИФункции