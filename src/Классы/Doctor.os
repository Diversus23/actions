#Использовать coloratos

// Есть ли ошибки при проверке
Перем ЕстьОшибки;

#Область ПрограммныйИнтерфейс

// BSLLS:UnusedParameters-off
// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	ПроверитьУстановленныеПрограммы();
    
КонецПроцедуры // ВыполнитьКоманду()
// BSLLS:UnusedParameters-on

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьУстановленныеПрограммы()
	
	ЕстьОшибки = Ложь;

	ВывестиРосчерк();
	ЦветнойВывод.ВывестиСтроку("Проверка программ необходимых для работы actions", "Синий");
	ВывестиРосчерк();

	Таблица = ОбщегоНазначения.ВыполнитьПроверкуСистемы();
	Для Каждого Проверка Из Таблица Цикл	
		ЦветнойВывод.ВывестиСтроку(ТекстПроверки(Проверка), "Белый");
	КонецЦикла;

	ВывестиРосчерк();

	Если ЕстьОшибки = Истина Тогда
		ЦветнойВывод.ВывестиСтроку("Не все проверки пройдены", "Желтый");
	Иначе
		ЦветнойВывод.ВывестиСтроку("Все проверки пройдены", "Зеленый");
	КонецЕсли;

КонецПроцедуры

Функция ТекстПроверки(Знач Проверка)

	Текст = "Приложение " + Проверка.ИмяПрограммы;
	Если Проверка.Успешно = Истина Тогда 
		Текст = "([+]|#color=Зеленый) " + Текст + " установлено";
	Иначе
		Текст = "([-]|#color=Красный) " + Текст + " не установлено. " + Проверка.ТекстНеУспешно;
		ЕстьОшибки = Истина;
	КонецЕсли;

	ТекстСсылки = "";
	Если ТипЗнч(Проверка.Ссылка) = Тип("Массив") Тогда
		Для Индекс = 0 По Проверка.Ссылка.Количество() - 1 Цикл
			Если НЕ ПустаяСтрока(ТекстСсылки) Тогда
				ТекстСсылки = ТекстСсылки + ", ";
			КонецЕсли;
			ТекстСсылки = ТекстСсылки + ОформлениеСсылки(Проверка.Ссылка[Индекс]);
		КонецЦикла;
	Иначе
		ТекстСсылки = ОформлениеСсылки(Проверка.Ссылка);
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ТекстСсылки) Тогда
		Текст = Текст + " " + ТекстСсылки;
	КонецЕсли;

	Возврат Текст;
	
КонецФункции

Функция ОформлениеСсылки(Знач Текст)

	Возврат "[(" + Текст + "|#color=Желтый)]";

КонецФункции

Процедура ВывестиРосчерк()

	ЦветнойВывод.ВывестиСтроку("================================================", "Синий");
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции