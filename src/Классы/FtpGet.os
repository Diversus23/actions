#Область ПрограммныйИнтерфейс

// Процедура - устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект описание команды
//
Процедура ОписаниеКоманды(Команда) Экспорт
	
	РаботаFTP.ОписаниеКоманды(Команда);
	
	ШаблонСтроки = "%1 %2";
	Текст = СтрШаблон(ШаблонСтроки,
			"Имя директории на FTP-сервере, что необходимо получить.",
			"Если не указано, то будет использован корневой путь ""/"" (необязательный)");
	Команда.Опция("remote r", "", Текст)
		.ТСтрока();
	
	Текст = СтрШаблон(ШаблонСтроки,
			"Маска файлов для скачивания. Если не указана, то используется маска ""*"" (необязательный).",
			"Пример: *.txt");
	Команда.Опция("mask m", "", Текст)
		.ТСтрока();
	
	Текст = СтрШаблон(ШаблонСтроки,
			"Директория на локальном компьютере куда необходимо загрузить файлы (обязательный).",
			"Пример: ""C:\Path\File.txt"", ""C:\Path\""");
	Команда.Опция("local", "", Текст)
		.ТСтрока();
	
	Текст = СтрШаблон(ШаблонСтроки,
			"Флаг рекурсивного копирования файлов с FTP-сервера (необязательный).",
			"По умолчанию копирование выполняется только с указанной на сервере директории");
	Команда.Опция("recursive r", "", Текст)
		.ТБулево();
	
КонецПроцедуры // ОписаниеКоманды()

// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	
	СтруктураПараметров = ПолучитьСтруктуруПараметров(Команда);
	РаботаFTP.ПолучениеФайлов(СтруктураПараметров);
	
КонецПроцедуры // ВыполнитьКоманду()

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСтруктуруПараметров(Знач Команда)
	
	ЧтениеОпций = Новый ЧтениеОпцийКоманды(Команда);
	ВыводОтладочнойИнформации = ЧтениеОпций.ЗначениеОпции("verbose");
	ПараметрыСистемы.УстановитьРежимОтладки(ВыводОтладочнойИнформации);
	
	ПараметрыКоманды = Новый Структура;
	ПараметрыКоманды.Вставить("Сервер", ЧтениеОпций.ЗначениеОпции("server", Истина));
	ПараметрыКоманды.Вставить("ИмяПользователя", ЧтениеОпций.ЗначениеОпции("login", Ложь));
	ПараметрыКоманды.Вставить("Пароль", ЧтениеОпций.ЗначениеОпции("password", Ложь));
	ПараметрыКоманды.Вставить("Порт", ЧтениеОпций.ЗначениеОпции("port", Ложь));
	ПараметрыКоманды.Вставить("Таймаут", ЧтениеОпций.ЗначениеОпции("timeout", Ложь));
	ПараметрыКоманды.Вставить("ПутьНаСервере", ЧтениеОпций.ЗначениеОпции("remote"));
	ПараметрыКоманды.Вставить("Маска", ЧтениеОпций.ЗначениеОпции("mask"));
	ПараметрыКоманды.Вставить("ПутьЛокально", ЧтениеОпций.ЗначениеОпции("local"));
	ПараметрыКоманды.Вставить("Рекурсивно", ЧтениеОпций.ЗначениеОпции("recursive"));
	
	Если НЕ ЗначениеЗаполнено(ПараметрыКоманды.ИмяПользователя)
		И НЕ ЗначениеЗаполнено(ПараметрыКоманды.Пароль) Тогда
		ПараметрыКоманды.ИмяПользователя = "anonymous";
		ПараметрыКоманды.Пароль = "anonymous";
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ПараметрыКоманды.Порт) Тогда
		ПараметрыКоманды.Порт = РаботаFTP.ПортПоУмолчанию();
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ПараметрыКоманды.Таймаут) Тогда
		ПараметрыКоманды.Таймаут = РаботаFTP.ТаймаутПоУмолчанию();
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ПараметрыКоманды.ПутьНаСервере) Тогда
		ПараметрыКоманды.ПутьНаСервере = "/";
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ПараметрыКоманды.Маска) Тогда
		ПараметрыКоманды.Маска = "*";
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ПараметрыКоманды.Рекурсивно) Тогда
		ПараметрыКоманды.Рекурсивно = Ложь;
	КонецЕсли;
	
	Возврат ПараметрыКоманды;
	
КонецФункции // ПолучитьСтруктуруПараметров()

#КонецОбласти // СлужебныеПроцедурыИФункции