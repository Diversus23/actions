#Использовать v8runner

#Область ПрограммныйИнтерфейс

// Процедура - устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект описание команды
//
Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Опция("path p", "",
		"Путь feature-файлах (обязательный)")
		.ТСтрока();

	Команда.Опция("tag t", "",
		"Тег, который необходимо найти во feature-файлах и посчитать количество таких тестов (обязательный)")
		.ТСтрока();

КонецПроцедуры // ОписаниеКоманды()

// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	
	Параметры = ПолучитьСтруктуруПараметров(Команда);

	Файлы = НайтиФайлы(Параметры.Путь, "*.feature", Истина);
	Количество = 0;
	Для Каждого Файл Из Файлы Цикл
		Если НайтиТег(Файл.ПолноеИмя, Параметры.Тег) Тогда
			Количество = Количество + 1;
		КонецЕсли;
	КонецЦикла;
	Сообщить(Количество);
	
КонецПроцедуры // ВыполнитьКоманду()

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСтруктуруПараметров(Знач Команда)
	
	ЧтениеОпций = Новый ЧтениеОпцийКоманды(Команда);
	ВыводОтладочнойИнформации = ЧтениеОпций.ЗначениеОпции("verbose");
	ПараметрыСистемы.УстановитьРежимОтладки(ВыводОтладочнойИнформации);
	
	ПараметрыКоманды = Новый Структура();
	ПараметрыКоманды.Вставить("Путь", ЧтениеОпций.ЗначениеОпции("path", Истина));
	ПараметрыКоманды.Вставить("Тег", ЧтениеОпций.ЗначениеОпции("tag", Истина));
	Если НЕ СтрНачинаетсяС(ПараметрыКоманды.Тег, "@") Тогда
		ПараметрыКоманды.Тег = "@" + ПараметрыКоманды.Тег;
	КонецЕсли;

	Возврат ПараметрыКоманды;
	
КонецФункции // ПолучитьСтруктуруПараметров()

Функция НайтиТег(Знач ИмяФайла, Знач Тег)
	
	нТег = НРег(СокрЛП(Тег));
	Текст = ФайловыеОперации.ПрочитатьТекстФайла(ИмяФайла);
	Для Индекс = 1 По СтрЧислоСтрок(Текст) Цикл
		Стр = СтрПолучитьСтроку(Текст, Индекс);
		Если НРег(СокрЛП(Стр)) = нТег Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;

	Возврат Ложь;

КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции