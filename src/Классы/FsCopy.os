#Область ПрограммныйИнтерфейс

// Процедура - устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект описание команды
//
Процедура ОписаниеКоманды(Команда) Экспорт
    
	Команда.Опция("path src from f", "",
		"Указывает расположение и имена файлов, которые требуется скопировать (обязательный)")
		.ТСтрока();

	Команда.Опция("destination dst to t", "",
		"Расположение или имена новых файлов (обязательный)")
		.ТСтрока();

	Команда.Опция("move delsrc m", "", 
		"Выполнить перемещение файлов (удалить источник после копирования) (не обязательный)")
		.Флаговый();

КонецПроцедуры // ОписаниеКоманды()

// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	
	Лог = ПараметрыСистемы.Лог();
	ПараметрыКоманды = ПолучитьСтруктуруПараметров(Команда);
	Лог.Информация("Начало копирования файлов из %1 в %2", ПараметрыКоманды.Источник, ПараметрыКоманды.Приемник);
	Результат = ФайловыеОперации.КопироватьФайлы(ПараметрыКоманды.Источник,
		ПараметрыКоманды.Приемник,
		Истина,
		ПараметрыКоманды.Переместить);
    
КонецПроцедуры // ВыполнитьКоманду()

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСтруктуруПараметров(Знач Команда)

	ЧтениеОпций = Новый ЧтениеОпцийКоманды(Команда);	
	ВыводОтладочнойИнформации = ЧтениеОпций.ЗначениеОпции("verbose");	
	ПараметрыСистемы.УстановитьРежимОтладки(ВыводОтладочнойИнформации);

	ПараметрыКоманды = Новый Структура;
	ПараметрыКоманды.Вставить("Источник", ЧтениеОпций.ЗначениеОпции("path", Истина));
	ПараметрыКоманды.Вставить("Приемник", ЧтениеОпций.ЗначениеОпции("destination", Истина));
	ПараметрыКоманды.Вставить("Переместить", ЧтениеОпций.ЗначениеОпции("move", Ложь));
	ПараметрыКоманды.Вставить("Переместить", ЗначениеЗаполнено(ПараметрыКоманды.Переместить));
	Возврат ПараметрыКоманды; 

КонецФункции // ПолучитьСтруктуруПараметров()

#КонецОбласти // СлужебныеПроцедурыИФункции