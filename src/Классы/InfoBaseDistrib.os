#Область ПрограммныйИнтерфейс

// Процедура - устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект описание команды
//
Процедура ОписаниеКоманды(Команда) Экспорт
			
	// Добавление дополнительных опций
	ПараметрыОпций = Работа1С.КонструкторСозданияДистрибутива();
	Работа1С.ОписаниеКоманды(Команда, ПараметрыОпций);
	
	Команда.Опция("path", "",
		"Каталог сборки дистрибутива (где будет создан дистрибутив) (обязательный)")
		.ТСтрока();	

	Команда.Опция("versions_path vp", "",
		"Каталог версий где будут браться версии конфигураций для создания обновлений (обязательный)")
		.ТСтрока();	

	Команда.Опция("versions_updates vu", "",
		"На какие версии будет устанавливаться текущая версия. Версии перечисляются через запятую (обязательный)")
		.ТСтрока();

	Команда.Опция("minimal_platform_version mpv", "",
		"Минимальная версия платформы 1С для работы (обязательный)")
		.ТСтрока();

	Команда.Опция("revoked_versions rv", "",
		"Какие версии были с отозваны (не обязательный)")
		.ТСтрока();

	Команда.Опция("shared_path sp", "",
		"Каталог общих файлов (не обязательный)")
		.ТСтрока();

	Команда.Опция("destination", "",
		"Назначение установки чистой базы 1С (обязательный)")
		.ТСтрока();
		
	Команда.Опция("destination_demo", "",
		"Назначение установки демо базы 1С (обязательный)")
		.ТСтрока();		 

КонецПроцедуры // ОписаниеКоманды()

// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	
	Параметры = ПолучитьСтруктуруПараметров(Команда);
	МенеджерКонфигуратора = Новый МенеджерКонфигуратора();
	МенеджерКонфигуратора.Инициализация(Параметры);
	МенеджерКонфигуратора.ПолучитьИнформациюОМетаданных();
	МенеджерКонфигуратора.СоздатьДистрибутив(Параметры);
	МенеджерКонфигуратора.Деструктор();
	
КонецПроцедуры // ВыполнитьКоманду()

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСтруктуруПараметров(Знач Команда)
	
	ЧтениеОпций = Новый ЧтениеОпцийКоманды(Команда);
	ВыводОтладочнойИнформации = ЧтениеОпций.ЗначениеОпции("verbose");
	ПараметрыСистемы.УстановитьРежимОтладки(ВыводОтладочнойИнформации);
	
	ПараметрыОпций = Работа1С.КонструкторСозданияДистрибутива();
	ПараметрыКоманды = Работа1С.ПрочитатьПараметры(Команда, ПараметрыОпций);	
	
	ПараметрыКоманды.Вставить("КаталогСборки", ЧтениеОпций.ЗначениеОпции("path", Истина));
	ПараметрыКоманды.Вставить("КаталогВерсий", ЧтениеОпций.ЗначениеОпции("versions_path", Истина));
	ПараметрыКоманды.Вставить("ВерсииОбновления", ЧтениеОпций.ЗначениеОпции("versions_updates", Истина));
	ПараметрыКоманды.Вставить("МинимальнаяВерсияПлатформы", ЧтениеОпций.ЗначениеОпции("minimal_platform_version", Истина));
	ПараметрыКоманды.Вставить("ОтозванныеВерсии", ЧтениеОпций.ЗначениеОпции("revoked_versions"));
	ПараметрыКоманды.Вставить("КаталогОбщихФайлов", ЧтениеОпций.ЗначениеОпции("shared_path"));
	ПараметрыКоманды.Вставить("Назначение", ЧтениеОпций.ЗначениеОпции("destination"));
	ПараметрыКоманды.Вставить("НазначениеДемо", ЧтениеОпций.ЗначениеОпции("destination_demo"));
	
	Возврат ПараметрыКоманды;
	
КонецФункции // ПолучитьСтруктуруПараметров()

#КонецОбласти // СлужебныеПроцедурыИФункции