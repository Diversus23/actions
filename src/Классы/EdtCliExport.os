#Область ПрограммныйИнтерфейс

// Процедура - устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект описание команды
//
Процедура ОписаниеКоманды(Команда) Экспорт
	
	ФорматСтроки = "%1 %2";
	
	Команда.Опция("project j", "",
		"Каталог, содержащий проект 1C:Enterprise Development Tools (обязательный)")
		.ТСтрока();
	
	Текст = СтрШаблон(ФорматСтроки,
		"Каталог куда необходимо сохранить файлы конфигурации платформы 1С:Предприятия в формате XML.",
		"Важно! Каталог будет очищен в процессе работы команды (обязательный)");
	Команда.Опция("path p", "", Текст)
		.ТСтрока();
		
	Команда.Опция("workspace w", "",
		"Каталог рабочего пространства для запуска 1C:Enterprise Development Tools (необязательный)")
		.ТСтрока();

	Текст = СтрШаблон("%1 %2", 
		"Версия EDT для экспорта. Пример: ""2021.1.5""",
		"Список установленных версий можно узнать набрав ""oscript actions.os edt cli versions"" (необязательный)");
	Команда.Опция("edtversion e", "", Текст)
		.ТСтрока();

	Команда.Опция("processpath", "",
		"Каталог записи информация о процессе EDT. Если процесс зависнет, то система закроет его (не обязательный)")
		.ТСтрока();
	
КонецПроцедуры // ОписаниеКоманды()

// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	
	ПараметрыКоманды = ПолучитьСтруктуруПараметров(Команда);
	РаботаEDT.ЭкспортВXML1CEdtCli(ПараметрыКоманды);
	
КонецПроцедуры // ВыполнитьКоманду()

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСтруктуруПараметров(Знач Команда)
	
	ЧтениеОпций = Новый ЧтениеОпцийКоманды(Команда);
	ВыводОтладочнойИнформации = ЧтениеОпций.ЗначениеОпции("verbose");
	ПараметрыСистемы.УстановитьРежимОтладки(ВыводОтладочнойИнформации);
	
	ПараметрыКоманды = Новый Структура;
	ПараметрыКоманды.Вставить("КаталогПроекта", ЧтениеОпций.ЗначениеОпции("project", Истина));
	ПараметрыКоманды.Вставить("КаталогЭкспорта", ЧтениеОпций.ЗначениеОпции("path", Истина));
	ПараметрыКоманды.Вставить("КаталогПространства", ЧтениеОпций.ЗначениеОпции("workspace", Ложь));
	ПараметрыКоманды.Вставить("ВерсияEDT", ЧтениеОпций.ЗначениеОпции("edtversion"));
	ПараметрыКоманды.Вставить("КаталогКонтроляОткрытыхПриложений", ЧтениеОпций.ЗначениеОпции("processpath"));

	ТекстОшибки = "Каталог ""%1"" (%2) не существует";
	Если НЕ ФайловыеОперации.КаталогСуществует(ПараметрыКоманды.КаталогПроекта) Тогда
		ОбщегоНазначения.ЗавершениеРаботыОшибка(ТекстОшибки, "--project", ПараметрыКоманды.КаталогПроекта);
	КонецЕсли;

	Возврат ПараметрыКоманды;
	
КонецФункции // ПолучитьСтруктуруПараметров()

#КонецОбласти // СлужебныеПроцедурыИФункции