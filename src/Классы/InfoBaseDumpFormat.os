#Область ПрограммныйИнтерфейс

// Процедура - устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект описание команды
//
Процедура ОписаниеКоманды(Команда) Экспорт
			
	// Добавление дополнительных опций
	ПараметрыОпций = Работа1С.КонструкторВыгрузкиИБ();
	Работа1С.ОписаниеКоманды(Команда, ПараметрыОпций);
	
	Команда.Опция("path p", "",
		"Путь к каталогу для выгрузки ИБ (обязательный)")
		.ТСтрока();

	Текст = СтрШаблон("%1 %2", "Префикс создаваемой выгрузки информационной базы.", 
		"По умолчанию равно имени базы для серверной ИБ и папке с ИБ для файловой (не обязательный)");
	Команда.Опция("prefix p", "", Текст)
		.ТСтрока();

	Команда.Опция("format f", "",
		"Формат сохранения выгрузки. По умолчанию: ""yyyy_MM_dd_ЧЧ_мм_сс"" => ""2023_06_27_03_00_00"" (обязательный)")
		.ТСтрока();

КонецПроцедуры // ОписаниеКоманды()

// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	
	ПараметрыКоманды = ПолучитьСтруктуруПараметров(Команда);
	МенеджерКонфигуратора = Новый МенеджерКонфигуратора();
	МенеджерКонфигуратора.Инициализация(ПараметрыКоманды);
	Если НЕ ЗначениеЗаполнено(ПараметрыКоманды.Префикс) Тогда
		ПараметрыКоманды.Вставить("Префикс", МенеджерКонфигуратора.ИмяИБ());
	КонецЕсли;
	ИмяФайла = МенеджерКонфигуратора.ИмяФайлаПоИмениИБНаДату(ПараметрыКоманды.Префикс, ПараметрыКоманды.Формат, "dt");
	ПутьКФайлу = ОбъединитьПути(ПараметрыКоманды.Каталог, ИмяФайла);
	ПараметрыКоманды.Вставить("ПутьКФайлу", ПутьКФайлу);	

	МенеджерКонфигуратора.ВыгрузитьИБ(ПараметрыКоманды);
	МенеджерКонфигуратора.Деструктор();
	
КонецПроцедуры // ВыполнитьКоманду()

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСтруктуруПараметров(Знач Команда)
	
	ЧтениеОпций = Новый ЧтениеОпцийКоманды(Команда);
	ВыводОтладочнойИнформации = ЧтениеОпций.ЗначениеОпции("verbose");
	ПараметрыСистемы.УстановитьРежимОтладки(ВыводОтладочнойИнформации);
	
	ПараметрыОпций = Работа1С.КонструкторВыгрузкиИБ();

	ПараметрыКоманды = Работа1С.ПрочитатьПараметры(Команда, ПараметрыОпций);	
	ПараметрыКоманды.Вставить("Каталог", ЧтениеОпций.ЗначениеОпции("path", Истина));
	ПараметрыКоманды.Вставить("Формат", ЧтениеОпций.ЗначениеОпции("format", Ложь, "yyyy_MM_dd_ЧЧ_мм_сс"));
	ПараметрыКоманды.Вставить("Префикс", ЧтениеОпций.ЗначениеОпции("prefix", Ложь));

	ФайловыеОперации.ОбеспечитьКаталог(ПараметрыКоманды.Каталог);
	
	Возврат ПараметрыКоманды;
	
КонецФункции // ПолучитьСтруктуруПараметров()

#КонецОбласти // СлужебныеПроцедурыИФункции