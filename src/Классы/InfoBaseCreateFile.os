#Область ПрограммныйИнтерфейс

// Процедура - устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект описание команды
//
Процедура ОписаниеКоманды(Команда) Экспорт
		
	Команда.Опция("path p", "",
		"Каталог, где необходимо создать файловую базу 1С (обязательный)")
		.ТСтрока();
	
	// Добавление дополнительных опций
	ПараметрыОпций = Работа1С.КонструкторСозданияФайловойБазы();
	Работа1С.ОписаниеКоманды(Команда, ПараметрыОпций);
	
КонецПроцедуры // ОписаниеКоманды()

// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	
	СтруктураПараметров = ПолучитьСтруктуруПараметров(Команда);
	МенеджерКонфигуратора = Новый МенеджерКонфигуратора();
	МенеджерКонфигуратора.Инициализация(СтруктураПараметров);
	МенеджерКонфигуратора.СоздатьФайловуюБазу(СтруктураПараметров);
	МенеджерКонфигуратора.Деструктор();
	
КонецПроцедуры // ВыполнитьКоманду()

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСтруктуруПараметров(Знач Команда)
	
	ЧтениеОпций = Новый ЧтениеОпцийКоманды(Команда);
	ВыводОтладочнойИнформации = ЧтениеОпций.ЗначениеОпции("verbose");
	ПараметрыСистемы.УстановитьРежимОтладки(ВыводОтладочнойИнформации);
	
	ПараметрыОпций = Работа1С.КонструкторСозданияФайловойБазы();

	ПараметрыКоманды = Работа1С.ПрочитатьПараметры(Команда, ПараметрыОпций);	
	ПараметрыКоманды.Вставить("КаталогБазы", ЧтениеОпций.ЗначениеОпции("path", Истина));
	ПараметрыКоманды.Вставить("СтрокаПодключения", "/F" + ПараметрыКоманды.КаталогБазы);
	
	Возврат ПараметрыКоманды;
	
КонецФункции // ПолучитьСтруктуруПараметров()

#КонецОбласти // СлужебныеПроцедурыИФункции