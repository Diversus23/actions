#Область ПрограммныйИнтерфейс

// Процедура - устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект описание команды
//
Процедура ОписаниеКоманды(Команда) Экспорт

	ТекстШаблона = "%1 %2";
	ПараметрыОпций = Работа1С.КонструкторDbgsOn();
	Работа1С.ОписаниеКоманды(Команда, ПараметрыОпций);

	Текст = СтрШаблон(ТекстШаблона,
		"Файл-флаг для запущенного сервера отладки dbgs.", 
		"По умолчанию ""dbgs.json"". После остановки сервера отладки файл будет удален (не обязательный)");
	Команда.Опция("file f", "", Текст)
		.ТСтрока();	

КонецПроцедуры // ОписаниеКоманды()

// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	
	Лог = ПараметрыСистемы.Лог();

	Лог.Информация("Остановка сервера отладки dbgs");
	Параметры = ПолучитьСтруктуруПараметров(Команда);
	Если НЕ ФайловыеОперации.ФайлСуществует(Параметры.ИмяФайла) Тогда
		Лог.Предупреждение("Файл-флаг для запущенного сервера отладки dbgs не найден. Остановить dbgs не удалось");
		Возврат;
	КонецЕсли;

	Настройки = ОбщегоНазначения.ПрочитатьФайлJSON(Параметры.ИмяФайла);
	Идентификатор = Настройки.Получить("pid");
	Порт = Настройки.Получить("port");
	Процесс = НайтиПроцессПоИдентификатору(Идентификатор);
	Если ЗначениеЗаполнено(Процесс) Тогда
		Процесс.Завершить();
		Лог.Информация("Сервер отладки остановлен PID %1 (порт %2)", Идентификатор, Порт);
		ФайловыеОперации.УдалитьФайлЕслиОнСуществует(Параметры.ИмяФайла);
	Иначе
		Лог.Предупреждение("Сервер отладки на порту %1 не найден с указанным PID (%2)", Порт, Идентификатор);
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьКоманду()

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСтруктуруПараметров(Знач Команда)
	
	ЧтениеОпций = Новый ЧтениеОпцийКоманды(Команда);
	ВыводОтладочнойИнформации = ЧтениеОпций.ЗначениеОпции("verbose");
	ПараметрыСистемы.УстановитьРежимОтладки(ВыводОтладочнойИнформации);
	
	ПараметрыКоманды = Новый Структура();
	ИмяФайлаПоУмолчанию = ОбъединитьПути(ОбщегоНазначения.КаталогПроекта(), "dbgs.json");
	ПараметрыКоманды.Вставить("ИмяФайла", ЧтениеОпций.ЗначениеОпции("file", Ложь, ИмяФайлаПоУмолчанию));

	Возврат ПараметрыКоманды;
	
КонецФункции // ПолучитьСтруктуруПараметров()

#КонецОбласти // СлужебныеПроцедурыИФункции