#Область ПрограммныйИнтерфейс

// Процедура - устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект описание команды
//
Процедура ОписаниеКоманды(Команда) Экспорт
    
	Команда.Опция("cmd c", "",
		"Командная строка запускаемого процесса. Пример ""notepad.exe"" или ""C:\Folder\File.exe"" (обязательный)")
		.ТСтрока();

	Команда.Опция("arg a", "",
		"Аргументы запускаемого процесса (не обязательный)")
		.ТСтрока();

	Команда.Опция("wait w", "",
		"Ожидать завершения. По умолчанию не ожидает (не обязательный)")
		.ТБулево();

	Команда.Опция("processpath p", "",
		"Каталог записи информация процессе EDT. Если процесс зависнет, то система закроет его (не обязательный)")
		.ТСтрока();

КонецПроцедуры

// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт
    
	Лог = ПараметрыСистемы.Лог();

	Параметры = ПолучитьСтруктуруПараметров(Команда);
    Команда = Новый КонтролируемыйПроцесс();
	Команда.УстановитьКаталогКонтроляОткрытыхПриложений(Параметры.КаталогКонтроляОткрытыхПриложений);
	КоманднаяСтрока = Параметры.КоманднаяСтрока;
	Если НЕ ПустаяСтрока(Параметры.Аргументы) Тогда
		КоманднаяСтрока = КоманднаяСтрока + " " + СокрЛП(Параметры.Аргументы);
	КонецЕсли;	
	ТекстОжидания = ?(Параметры.ОжидатьЗавершения, "и ожидаем завершения", "");
	Лог.Информация("Выполнение команды: ""%1"" %2", КоманднаяСтрока, ТекстОжидания);
	Команда.УстановитьСтрокуЗапуска(КоманднаяСтрока);
	Если Параметры.ОжидатьЗавершения Тогда
		Команда.Исполнить();
	Иначе
		Команда.ЗапуститьПроцесс();
	КонецЕсли;
	Лог.Информация("Команда выполнена");
        
КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСтруктуруПараметров(Знач Команда)

	ЧтениеОпций = Новый ЧтениеОпцийКоманды(Команда);	
	ВыводОтладочнойИнформации = ЧтениеОпций.ЗначениеОпции("verbose");	
	ПараметрыСистемы.УстановитьРежимОтладки(ВыводОтладочнойИнформации);

	ПараметрыКоманды = Новый Структура;
	ПараметрыКоманды.Вставить("КоманднаяСтрока", ЧтениеОпций.ЗначениеОпции("cmd", Истина));
	ПараметрыКоманды.Вставить("Аргументы", ЧтениеОпций.ЗначениеОпции("arg"));
	ПараметрыКоманды.Вставить("ОжидатьЗавершения", ЧтениеОпций.ЗначениеОпции("wait"));
	ПараметрыКоманды.ОжидатьЗавершения = ЗначениеЗаполнено(ПараметрыКоманды.ОжидатьЗавершения);
	ПараметрыКоманды.Вставить("КаталогКонтроляОткрытыхПриложений", ЧтениеОпций.ЗначениеОпции("processpath"));

	Возврат ПараметрыКоманды;

КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции