Процедура ЗарегистрироватьКоманду(Знач ИмяКоманды, Знач Парсер) Экспорт
	
	ОписаниеКоманды = Парсер.ОписаниеКоманды(ИмяКоманды, "Создать zip-архив");
	Парсер.ДобавитьКоманду(ОписаниеКоманды);

    Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, 
    	"-file",
    	"Полное имя файла создаваемого zip-архива (обязательный)");	
	Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, 
    	"-path",
    	"Файлы и папки, включаемые в архив. Можно использовать разделитель "";"" или ""|"" и маски файлов (обязательный).
		| Примеры:
		|   ""C:\Path\File1.txt|C:\Path\Folder""
		|   ""C:\Path\*.txt;C:\Path\Folder\*.*""
		|   ""C:\Path\Folder""");
	Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, 
    	"-p",
    	"Пароль для архива (необязательный)");

КонецПроцедуры

Функция ВыполнитьКоманду(Знач Параметры, Знач ДополнительныеПараметры = Неопределено) Экспорт

	ВозможныйРезультат = МенеджерКомандПриложения.РезультатыКоманд();
	Лог = ДополнительныеПараметры.Лог;
	Лог.Информация("Начало создания ZIP-архива");

	ИмяФайлаАрхива = Параметры["-file"];
	МаскаДобавляемыхФайлов = Параметры["-path"];
	Пароль = Параметры["-p"];
	
	Если ПустаяСтрока(ИмяФайлаАрхива) Тогда
		Лог.Ошибка("Не задано полое имя создаваемого zip-архива (-file)");
		Возврат ВозможныйРезультат.НеверныеПараметры;
	КонецЕсли;

	Если ПустаяСтрока(Параметры["-path"]) Тогда
		Лог.Ошибка("Не заданы файлы и папки для включения в архив (-path)");
		Возврат ВозможныйРезультат.НеверныеПараметры;
	КонецЕсли;		

	ЗаписьZIP =  Новый ЗаписьZipФайла(ИмяФайлаАрхива, Пароль);

	РазделительФайлов = ?(Найти(МаскаДобавляемыхФайлов, "|") > 0, "|", ";");
	МассивМасок = СтроковыеФункции.РазложитьСтрокуВМассивПодстрок(МаскаДобавляемыхФайлов, 
		РазделительФайлов, Истина, Истина);
	Для Каждого МаскаДобавляемогоФайлаПапки Из МассивМасок Цикл
		Лог.Информация("Добавление файла (каталога) <" +  МаскаДобавляемогоФайлаПапки + ">");
		ЗаписьZIP.Добавить(МаскаДобавляемогоФайлаПапки,
			РежимСохраненияПутейZIP.СохранятьОтносительныеПути,
			РежимОбработкиПодкаталоговZIP.ОбрабатыватьРекурсивно);
	КонецЦикла;
	ЗаписьZIP.Записать();

	Лог.Информация("Создание архива завершено");

    Возврат ВозможныйРезультат.Успех;

КонецФункции